bundle agent enable_cfengine
{
vars:
 freebsd::
  "rc_conf[cfengine3_enable]" string => '"YES"';
 
 linux::
  "rc_files" slist => {
                       "/etc/rc1.d/K11cfengine3",
                       "/etc/rc2.d/S90cfengine3",
                       "/etc/rc3.d/S90cfengine3",
                       "/etc/rc4.d/S90cfengine3",
                       "/etc/rc5.d/S90cfengine3",
                       "/etc/rc6.d/K11cfengine3"
                       };
 
files:
 freebsd::
  "/usr/local/etc/rc.d/cfengine3.sh"
     comment => "Ensure that the cfengine rc.d script is executable",
     perms => mog("755", "root", "wheel");
 
  "/etc/rc.conf"
     comment => "cfengine libraries should be enabled in rc.conf if appropriate",  
     edit_line => append_if_no_line("[ -e /var/cfengine/lib ] && /sbin/ldconfig -m /var/cfengine/lib");

  "/etc/rc.conf"
     comment => "cfengine should be enabled in rc.conf",
     edit_line => set_variable_values("enable_cfengine.rc_conf");

 linux::
  "$(rc_files)"
     comment => "Ensure that startup scripts are properly linked",
     link_from => ln_s("/etc/init.d/cfengine3");
}
