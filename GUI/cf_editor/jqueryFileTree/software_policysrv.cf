##########################################################################
#
#   software_policysrv.cf - Install Required Software on the Policy Server
#
##########################################################################


bundle agent software_policysrv

{
 vars:

  SuSE.policy_host::

   "match_package" slist => { 
                             "apache2", 
                             "apache2-mod_php5",
                             "apache2-prefork",
                             "mysql",
                             "php5" 
                            };

  redhat.policy_host::

   "match_package" slist => {
                             "httpd",
                             "mod_ssl",
                             "mysql",
                             "mysql-server",
                             "php"
                            };

 debian.policy_host::

  "match_package" slist => {
                            "apache2",
                            "mysql-server",
                            "php5",
                            "ssl-cert"
                           };

 packages:

  SuSE.policy_host::

   "$(match_package)"

      package_policy => "add",
      package_method => zypper,
      classes        => if_repaired("a2enmod_ssl");

  redhat.policy_host::

   "$(match_package)"

      package_policy => "add",
      package_method => yum;

  debian.policy_host::

   "$(match_package)"

      package_policy => "add",
      package_method => apt,
      classes        => if_repaired("a2enmod_php5_ssl");

 commands:

  a2enmod_php5_ssl::

   "/usr/sbin/a2enmod php5 ssl",
      classes => if_repaired("restart_apache2");

#  restart_apache2::
#
#   "/etc/init.d/apache2 restart";

  a2enmod_ssl::

   "/usr/sbin/a2enmod ssl && /usr/sbin/a2enflag SSL && /usr/bin/gensslcert";

  a2ensite_ssl::

   "/usr/sbin/a2ensite ssl";

 files:

  debian.policy_host::

   "/var/www/."

      delete => tidy,
      file_select => by_name("index.html"),
      depth_search => recurse("1");

   "/etc/apache2/sites-available/ssl"
      copy_from => local_cp("/etc/apache2/sites-available/default-ssl"),
      action => immediate,
      classes => if_repaired("a2ensite_ssl");

  SuSE.policy_host::

   "/etc/apache2/vhosts.d/vhost-ssl.conf"

      copy_from => local_cp("/etc/apache2/vhosts.d/vhost-ssl.template"),
      action => immediate;

}
