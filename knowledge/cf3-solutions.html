<html lang="en">
<head>
<title>CFEngine Solutions</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="CFEngine Solutions">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="top" href="#Top">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
@font-face {
    font-family: 'CFE_FONT';
    src: url('fonts/eot/opensans-regular-webfont.eot');
    src: local('â˜º'),  url('fonts/ttf/opensans-regular-webfont.ttf') format('truetype'), url('fonts/svg/opensans-regular-webfont.svg') format('svg');
    font-weight: normal;
    font-style: normal;
}    
pre {
    background-color: #EEFFDD;
    border: 1px solid #CCCCCC;
    font-family: courier;
    margin-bottom: 10px;
    margin-top: 10px;
    padding: 5px;
    font-size: 90%;
    }
pre.display { font-family:inherit }
pre.format  { font-family:inherit }
pre.smallexample
pre.smalllisp,
pre.smallformat,
pre.smalldisplay {
  font-size: 90%;
} 

span.sc    { font-variant:small-caps }
span.roman { font-family:serif; font-weight:normal; } 
span.sansserif { font-family:sans-serif; font-weight:normal; } 

body {
    font:  90%  'CFE_FONT', arial, Helvetica,sans-serif; 
    color: #646464;
    padding: 10px 20px;
    width: 960px;
    margin: 0 auto;
}
.node
{
    text-align: right;
    padding: 2px;
    font-size: smaller;
}
.node hr {
    border: 0;
    width: 100%;
    color: #CCC;
    background-color: #CCC;
    height: 5px;
}
.section {
    padding-right: 0px;
    padding-bottom: 0px;
    padding-left: 0px;
}

h1 {
    font-size: 26px;
    font-weight: normal;
    line-height: 32px;
    margin: 32px 0 16px;
    text-align: left;
    text-transform: uppercase;
}

h2 {
    color: #9E9981 !important;
    font-size: 16px;
    line-height: 18px;
    font-weight: normal;
    margin: 16px 0 26px;
    text-align: left;
}
h3 {
    margin-top: 3px;
    margin-right: 0px;
    margin-bottom: 10px;
    margin-left: 0px;
    line-height: 20px;
    font-size: 16px;
    font-weight: normal;
}

.contents
{
    background-color: #CCC;
    padding-top: 2px;
    padding-right: 2px;
    padding-bottom: 2px;
    padding-left: 10px;
}

.index-cp
{  
background: #fff url(index-cp.png) right repeat-y;
}

.index-vr
{  
background: #fff url(index-vr.png) right repeat-y;
}

.index-mb
{  
background: #eee url(index-faq.png) right repeat-y;
}

table.border
{
	border-color: #666;
	border-width: 0px;
}

FONT.liten {font-size: 80%; }
 
.tynn {
    font-family: Arial, Helvetica, sans-serif;
    font-size: smaller;
    font-style: normal;
    font-weight: lighter;
    margin-bottom: 0em;
    font-size: 11pt;
}
.verbatim {
    color: #000;
    margin-top: 0px;
    margin-right: 0px;
    margin-bottom: 20px;
    margin-left: 0px;
}
.example {
    color: #000;
    width: 100%;
    margin-top: 0px;
    margin-right: 0px;
    margin-bottom: 20px;
    margin-left: 0px;
}
.smallexample {
    color: #000;
    padding-top: 10px;
    padding-right: 30px;
    padding-bottom: 5px;
    padding-left: 30px;
    margin-top: 0px;
    margin-right: 0px;
    margin-bottom: 0px;
    margin-left: 0px;
}
.cartouche {
    padding: 5px;
    font-style: italic;
    font-size: 85%;
}

table.cartouche {
    border: none !important;
}

 .cartouche td  {
    background-color: #ddd;
    border: 1px solid #ccc;
    padding: 5px;
}

A:link { color: #2c2e70 }
A:visited { color: black }
A:active { color: #600041 }
dt em {font-weight: bold}
/* don't change this rule */    
pre.sp {
    background: none !important;   
    border:none !important
}
/* --- */
/*code hightlight*/
.red { color: #b80047; font-weight: bold; }

.blue { color: blue;  /*font-weight: bold;*/ }

.green { color: darkgreen; }

.comment { font-style: italic; }
--></style>
</head>
<body>
<h1 class="settitle">CFEngine Solutions</h1>
<div class="node">
<a name="Top"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Introduction">Introduction</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#dir">(dir)</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#dir">(dir)</a>

</div>

<h2 class="unnumbered">CFEngine-Solutions</h2>

   <p><a name="Contents">
   <div class="contents">
<h2>Table of Contents</h2>
<ul>
<li><a name="toc_Top" href="#Top">CFEngine-Solutions</a>
<li><a name="toc_Introduction" href="#Introduction">1 Introduction</a>
<ul>
<li><a href="#Begin-_002d-Get-started">1.1 Begin - Get Started</a>
<li><a href="#Create-files-and-directories">1.2 Create files and directories</a>
<li><a href="#Copy-single-files">1.3 Copy single files</a>
<li><a href="#Copy-directory-trees">1.4 Copy directory trees</a>
<li><a href="#Editing-password-or-group-files">1.5 Editing password or group files</a>
<li><a href="#Editing-password-or-group-files-custom">1.6 Editing password or group files custom</a>
<li><a href="#Disabling-and-rotating-files">1.7 Disabling and rotating files</a>
<li><a href="#Hashing-for-change-detection-_002d-tripwire">1.8 Hashing for change detection (tripwire)</a>
<li><a href="#Command-or-script-execution">1.9 Command or script execution</a>
<li><a href="#Kill-process">1.10 Kill process</a>
<li><a href="#Restart-process">1.11 Restart process</a>
<li><a href="#Check-filesystem-space">1.12 Check filesystem space</a>
<li><a href="#Mount-a-filesystem">1.13 Mount a filesystem</a>
<li><a href="#Software-and-patch-installation">1.14 Software and patch installation</a>
</li></ul>
<li><a name="toc_High-level" href="#High-level">2 High level</a>
<ul>
<li><a href="#Centralized-Management">2.1 Centralized Management</a>
<li><a href="#All-hosts-the-same">2.2 All hosts the same</a>
<li><a href="#Variation-in-hosts">2.3 Variation in hosts</a>
<li><a href="#Updating-from-a-central-hub">2.4 Updating from a central hub</a>
<li><a href="#Change-detection">2.5 Change detection</a>
<li><a href="#Garbage-collection">2.6 Garbage collection</a>
<li><a href="#Distribute-root-passwords">2.7 Distribute root passwords</a>
<li><a href="#Distribute-ssh-keys">2.8 Distribute ssh keys</a>
<li><a href="#Laptop-support-configuration">2.9 Laptop support configuration</a>
<li><a href="#Find-MAC-address">2.10 Find the MAC address</a>
<li><a href="#Log-rotation">2.11 Log rotation</a>
<li><a href="#Manage-a-system-file">2.12 Manage a system file</a>
<li><a href="#Simple-template">2.13 Simple template</a>
<li><a href="#Simple-versioned-template">2.14 Simple versioned template</a>
<li><a href="#Macro-template">2.15 Macro template</a>
<li><a href="#Custom-editing">2.16 Custom editing</a>
<li><a href="#Manage-a-system-process">2.17 Manage a system process</a>
<li><a href="#Ensure-running">2.18 Ensure running</a>
<li><a href="#Ensure-not-running">2.19 Ensure not running</a>
<li><a href="#Prune-processes">2.20 Prune processes</a>
<li><a href="#Manage-users">2.21 Manage users</a>
<li><a href="#Add-users">2.22 Add users</a>
<li><a href="#Remove-users">2.23 Remove users</a>
<li><a href="#Postfix-mail-configuration">2.24 Postfix mail configuration</a>
<li><a href="#Set-up-HPC-clusters">2.25 Set up HPC clusters</a>
<li><a href="#Set-up-name-resolution">2.26 Set up name resolution</a>
<li><a href="#Set-up-sudo">2.27 Set up sudo</a>
<li><a href="#Set-up-a-web-server">2.28 Set up a web server</a>
<li><a href="#Templating">2.29 Templating</a>
</li></ul>
<li><a name="toc_Low-level" href="#Low-level">3 Low level</a>
<ul>
<li><a href="#Aborting-execution">3.1 Aborting execution</a>
<li><a href="#ACL-file-example">3.2 ACL file example</a>
<li><a href="#ACL-generic-example">3.3 ACL generic example</a>
<li><a href="#ACL-secret-example">3.4 ACL secret example</a>
<li><a href="#Active-directory-example">3.5 Active directory example</a>
<li><a href="#Active-list-users-directory-example">3.6 Active list users directory example</a>
<li><a href="#Active-directory-show-users-example">3.7 Active directory show users example</a>
<li><a href="#Add-lines-to-a-file">3.8 Add lines to a file</a>
<li><a href="#Add-users-to-passwd-and-group">3.9 Add users to passwd and group</a>
<li><a href="#Add-software-packages-to-the-system">3.10 Add software packages to the system</a>
<li><a href="#Add-variable-definitions-to-a-file">3.11 Add variable definitions to a file e.g. <samp><span class="file">/etc/system</span></samp></a>
<li><a href="#Application-baseline">3.12 Application baseline</a>
<li><a href="#Array-example">3.13 Array example</a>
<li><a href="#Back-references-in-filenames">3.14 Backreferences in filenames</a>
<li><a href="#BSD-flags">3.15 BSD flags</a>
<li><a href="#Change-directory-for-command">3.16 Change directory for command</a>
<li><a href="#Check-file-or-directory-permissions">3.17 Check file or directory permissions</a>
<li><a href="#Class-match-example">3.18 Class match example</a>
<li><a href="#Client_002dserver-example">3.19 Client-server example</a>
<li><a href="#Commands-example">3.20 Commands example</a>
<li><a href="#Commenting-lines-in-a-file">3.21 Commenting lines in a file</a>
<li><a href="#Copy-files">3.22 Copy files</a>
<li><a href="#Copy-and-flatten-directory">3.23 Copy and flatten directory</a>
<li><a href="#Copy-then-edit">3.24 Copy then edit a file convergently</a>
<li><a href="#Creating-files-and-directories">3.25 Creating files and directories</a>
<li><a href="#Database-creation">3.26 Database creation</a>
<li><a href="#Deleting-lines-from-a-file">3.27 Deleting lines from a file</a>
<li><a href="#Deleting-lines-exception">3.28 Deleting lines exception</a>
<li><a href="#Editing-files">3.29 Editing files</a>
<li><a href="#Editing-tabular-files">3.30 Editing tabular files</a>
<li><a href="#Environment-_0028virtual_0029">3.31 Environments (virtual)</a>
<li><a href="#Environment-variables">3.32 Environment variables</a>
<li><a href="#Execresult-example">3.33 Execresult example</a>
<li><a href="#Inserting-lines-in-a-file">3.34 Inserting lines in a file</a>
<li><a href="#Get-a-list-of-users">3.35 Get a list of users</a>
<li><a href="#Global-classes">3.36 Global classes</a>
<li><a href="#Hello-world">3.37 Hello world</a>
<li><a href="#LDAP-interactions">3.38 LDAP interactions</a>
<li><a href="#Linking-files">3.39 Linking files</a>
<li><a href="#Listing-files_002dpattern-in-a-directory">3.40 Listing files-pattern in a directory</a>
<li><a href="#Locate-and-transform-files">3.41 Locate and transform files</a>
<li><a href="#Logging">3.42 Logging</a>
<li><a href="#Measurements">3.43 Measurements</a>
<li><a href="#Methods">3.44 Methods</a>
<li><a href="#Method-validation">3.45 Method validation</a>
<li><a href="#Mount-NFS-filesystem">3.46 Mount NFS filesystem</a>
<li><a href="#Ordering-promises">3.47 Ordering promises</a>
<li><a href="#Process-management">3.48 Process management</a>
<li><a href="#Read-from-a-TCP-socket">3.49 Read from a TCP socket</a>
<li><a href="#Resolver-management">3.50 Resolver management</a>
<li><a href="#Search-and-replace-text">3.51 Search and replace text</a>
<li><a href="#Selecting-a-region-in-a-file">3.52 Selecting a region in a file</a>
<li><a href="#Service-management-_0028windows_0029">3.53 Service management (windows)</a>
<li><a href="#Set-up-a-PXE-boot-server">3.54 Set up a PXE boot server</a>
<li><a href="#Tidying-garbage-files">3.55 Tidying garbage files</a>
<li><a href="#Software-distribution">3.56 Software distribution</a>
<li><a href="#Trigger-classes">3.57 Trigger classes</a>
<li><a href="#Unmount-NFS-filesystem">3.58 Unmount NFS filesystem</a>
<li><a href="#Web-server-modules">3.59 Web server modules</a>
<li><a href="#Warn-if-matching-line-in-file">3.60 Warn if matching line in file</a>
<li><a href="#Windows-registry">3.61 Windows registry</a>
<li><a href="#unit_005fregistry_005fcache_002ecf">3.62 unit_registry_cache.cf</a>
<li><a href="#unit_005fregistry_002ecf">3.63 unit_registry.cf</a>
</li></ul>
</li></ul>
</div>



<ul class="menu">
<li><a accesskey="1" href="#Introduction">Introduction </a>
<li><a accesskey="2" href="#High-level">High level</a>
<li><a accesskey="3" href="#Low-level">Low level</a>
</ul>

<!-- ********************************************************************** -->
<!-- CHAPTER -->
<!-- ********************************************************************** -->
<div class="node">
<a name="Introduction"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#High-level">High level</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Top">Top</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Top">Top</a>

</div>

<h2 class="chapter">1 Introduction</h2>

<p>CFEngine 3 has been redesigned to allow modular solution building in
terms of a simple, regular language.  This guide explains how to use
the CFEngine Community Open Promise-Body Library to express some
simple idioms and approaches for solving common system problems.

   <p>The solutions presented here are necessarily generic and incomplete,
since local environmental issues always define the boundaries of a
solution in a real setting.  A solution guide does not present
finished solutions, but rather hints about how to achieve such
solutions with standardized idioms.

   <p>CFEngine gives you great freedom to craft specific solutions, without
the need for low level coding. For most tasks, you will find the
standardized templates sufficient for your needs, but the possibilties
do not stop there.

   <p>The following resources are available as a supplement to this guide:

     <ul>
<li>Community Open Promise-Body Library, the nuts'n'bolts definitions for the solutions presented here: <a href="http://cfengine.com/manuals/CfengineStdLibrary.html">http://cfengine.com/manuals/CfengineStdLibrary.html</a>
<li>Tutorial: <a href="http://cfengine.com/manuals/cf-manuals/cf3-tutorial.html">http://cfengine.com/manuals/cf-manuals/cf3-tutorial.html</a>
<li>Reference manual: <a href="http://cfengine.com/manuals/cf-manuals/cf3-reference.html">http://cfengine.com/manuals/cf-manuals/cf3-reference.html</a>
</ul>

   <p>If you need assistance in formulating specific solutions for your
system environment, contact CFEngine's Professional Services at
<code>contact@CFEngine.com</code>;

<ul class="menu">
<li><a accesskey="1" href="#Begin-_002d-Get-started">Begin - Get started</a>
<li><a accesskey="2" href="#Create-files-and-directories">Create files and directories</a>
<li><a accesskey="3" href="#Copy-single-files">Copy single files</a>
<li><a accesskey="4" href="#Copy-directory-trees">Copy directory trees</a>
<li><a accesskey="5" href="#Editing-password-or-group-files">Editing password or group files</a>
<li><a accesskey="6" href="#Editing-password-or-group-files-custom">Editing password or group files custom</a>
<li><a accesskey="7" href="#Disabling-and-rotating-files">Disabling and rotating files</a>
<li><a accesskey="8" href="#Hashing-for-change-detection-_002d-tripwire">Hashing for change detection - tripwire</a>
<li><a accesskey="9" href="#Command-or-script-execution">Command or script execution</a>
<li><a href="#Kill-process">Kill process</a>
<li><a href="#Restart-process">Restart process</a>
<li><a href="#Check-filesystem-space">Check filesystem space</a>
<li><a href="#Mount-a-filesystem">Mount a filesystem</a>
<li><a href="#Software-and-patch-installation">Software and patch installation</a>
</ul>

<div class="node">
<a name="Begin---Get-started"></a>
<a name="Begin-_002d-Get-started"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Create-files-and-directories">Create files and directories</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Introduction">Introduction</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.1 Begin - Get Started</h3>

<p>To get started with CFEngine, you can imagine the following template for entering examples. 
This part of the code is common to all the examples. We include below for completeness,
but you might also like to hide is in yours.

<pre class="verbatim">
body common control
{
bundlesequence => { "main" };
inputs => { "cfengine_stdlib.cf" };
}


bundle agent main
{
# example
}

</pre>
Then you enter the cases as below. The general pattern
of the syntax is like this (red: CFEngine word, blue: user-defined word):

<pre class="verbatim"># The general pattern

bundle component name(parameters)
{ 
what_type:
 where_when::

  # Traditional comment

  "promiser" -> { "promisee1", "promisee2" },
        comment => "The intention ...",
         handle => "unique_id_label",
    attribute_1 => body_or_value1,
    attribute_2 => body_or_value2;
}

</pre>

<!-- ...................................... -->
<ul class="menu">
<li><a accesskey="1" href="#Create-files-and-directories">Create files and directories</a>
<li><a accesskey="2" href="#Copy-single-files">Copy single files</a>
<li><a accesskey="3" href="#Copy-directory-trees">Copy directory trees</a>
<li><a accesskey="4" href="#Editing-password-or-group-files">Editing password or group files</a>
<li><a accesskey="5" href="#Editing-password-or-group-files-custom">Editing password or group files custom</a>
<li><a accesskey="6" href="#Disabling-and-rotating-files">Disabling and rotating files</a>
<!-- * unit_disable_and_rotate_files.cf:: -->
<!-- * unit_disable.cf:: -->
<li><a accesskey="7" href="#Hashing-for-change-detection-_002d-tripwire">Hashing for change detection - tripwire</a>
<li><a accesskey="8" href="#Command-or-script-execution">Command or script execution</a>
<li><a accesskey="9" href="#Kill-process">Kill process</a>
<li><a href="#Restart-process">Restart process</a>
<li><a href="#Check-filesystem-space">Check filesystem space</a>
<li><a href="#Mount-a-filesystem">Mount a filesystem</a>
<li><a href="#Software-and-patch-installation">Software and patch installation</a>
</ul>

<div class="node">
<a name="Create-files-and-directories"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Copy-single-files">Copy single files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Begin-_002d-Get-started">Begin - Get started</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.2 Create files and directories</h3>

<p>Create files and directories and set permissions.

<pre class="verbatim">
########################################################
#
# Simple test create files
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/test_plain" 

       perms => system,
       create => "true";

  "/home/mark/tmp/test_dir/." 

       perms => system,
       create => "true";

}

#########################################################

body perms system

{
mode  => "0640";
}

#########################################################

body depth_search recurse(d)

{
depth => "$(d)";
}

</pre>

<!-- ...................................... -->
<div class="node">
<a name="Copy-single-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Copy-directory-trees">Copy directory trees</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Create-files-and-directories">Create files and directories</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.3 Copy single files</h3>

<p>Copy single files, locally (<code>local_cp</code>) or from a remote site (<code>secure_cp</code>). The Community Open Promise-Body Library (COPBL; <samp><span class="file">cfengine_stdlib.cf</span></samp>) should be included in the <samp><span class="file">/var/cfengine/inputs/</span></samp> directory and input as below.

<pre class="verbatim">
body common control
{
bundlesequence  => { "mycopy" };
inputs => { "cfengine_stdlib.cf" };
}

bundle agent mycopy
{
files:

  "/home/mark/tmp/test_plain"

    copy_from => local_cp("$(sys.workdir)/bin/file");

  "/home/mark/tmp/test_remote_plain"

    copy_from => secure_cp("$(sys.workdir)/bin/file","serverhost");
}


</pre>

<!-- ...................................... -->
<div class="node">
<a name="Copy-directory-trees"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Editing-password-or-group-files">Editing password or group files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Copy-single-files">Copy single files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.4 Copy directory trees</h3>

<p>Copy directory trees, locally (<code>local_cp</code>) or from a remote site (<code>secure_cp</code>). (<code>depth_search =&gt; recurse("")</code>) defines the number of sublevels to include, (<code>"inf"</code>) gets entire tree.

<pre class="verbatim">
body common control
{
bundlesequence  => { "my_recursive_copy" };
inputs => { "cfengine_stdlib.cf" };
}

bundle agent my_recursive_copy
{
files:

  "/home/mark/tmp/test_dir"

      copy_from => local_cp("$(sys.workdir)/bin/."),
   depth_search => recurse("inf");

  "/home/mark/tmp/test_dir"

      copy_from => secure_cp("$(sys.workdir)/bin","serverhost"),
   depth_search => recurse("inf");

}


</pre>

<!-- ...................................... -->
<div class="node">
<a name="Editing-password-or-group-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Editing-password-or-group-files-custom">Editing password or group files custom</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Copy-directory-trees">Copy directory trees</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.5 Editing password or group files</h3>

<p>To change the password of a system, we need to edit a file. A file is
a complex object &ndash; once open there is a new world of possible
promises to make about its contents.  CFEngine has bundles of promises
that are specially for editing.

<pre class="verbatim">

body common control
{
bundlesequence => { "edit_passwd" };
}

bundle agent edit_passwd
{

vars:

 "userset" slist => { "user1", "user2", "user3" };

files:

  "/etc/passwd"
     edit_line => 
        set_user_field("mark","7","/set/this/shell");


  "/etc/group"
     edit_line => 
        append_user_field("root","4","@(main.userset)");

}

</pre>

<!-- ...................................... -->
<div class="node">
<a name="Editing-password-or-group-files-custom"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Disabling-and-rotating-files">Disabling and rotating files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Editing-password-or-group-files">Editing password or group files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.6 Editing password or group files custom</h3>

<p>In this example the bundles from the Community Open Promise-Body Library are included directly in the policy instead of being input as a separate file.

<pre class="verbatim">

body common control
{
bundlesequence => { "addpasswd" };
}

bundle agent addpasswd
{
vars:

  # want to set these values by the names of their array keys

  "pwd[mark]" string => "mark:x:1000:100:Mark Burgess:/home/mark:/bin/bash";
  "pwd[fred]" string => "fred:x:1001:100:Right Said:/home/fred:/bin/bash";
  "pwd[jane]" string => "jane:x:1002:100:Jane Doe:/home/jane:/bin/bash";

files:


  "/tmp/passwd"

        create => "true",
     edit_line => append_users_starting("addpasswd.pwd");

}

############################################################
# Library stuff
############################################################

bundle edit_line append_users_starting(v)

{
vars:

  "index"        slist => getindices("$(v)");

classes:

  "add_$(index)" not => userexists("$(index)");

insert_lines:

  "$($(v)[$(index)])",

      ifvarclass => "add_$(index)";

}

############################################################

bundle edit_line append_groups_starting(v)

{
vars:

  "index"        slist => getindices("$(v)");

classes:

  "add_$(index)" not => groupexists("$(index)");

insert_lines:

  "$($(v)[$(index)])",

      ifvarclass => "add_$(index)";

}
</pre>

<!-- ...................................... -->
<div class="node">
<a name="Disabling-and-rotating-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Hashing-for-change-detection-_002d-tripwire">Hashing for change detection - tripwire</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Editing-password-or-group-files-custom">Editing password or group files custom</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.7 Disabling and rotating files</h3>

<p>Use the following simple steps to disable and rotate files. See the Community Open Promise-Body Library if you wish more details on what <code>disable</code> and <code>rotate</code> does.

<pre class="verbatim">
body common control
{
bundlesequence  => { "my_disable" };
inputs => { "cfengine_stdlib.cf" };
}

bundle agent my_disable
{

files:

  "/home/mark/tmp/test_create"
      rename => disable;
 
 "/home/mark/tmp/rotate_my_log"
      rename => rotate("4");

}


</pre>

<!-- ...................................... -->
<div class="node">
<a name="Hashing-for-change-detection---tripwire"></a>
<a name="Hashing-for-change-detection-_002d-tripwire"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Command-or-script-execution">Command or script execution</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Disabling-and-rotating-files">Disabling and rotating files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.8 Hashing for change detection (tripwire)</h3>

<p>Change detection is a powerful and easy way to monitor your environment, increase awareness and harden your system against security breaches.

<pre class="verbatim">
########################################################
#
# Change detect
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/web" -> "me"

   changes      => detect_all_change,
   depth_search => recurse("inf");
}

#########################################################

body changes detect_all_change

{
report_changes => "all";  
update_hashes  => "true";
}

#########################################################

body depth_search recurse(d)

{
depth        => "$(d)";
}
</pre>

<!-- ...................................... -->
<div class="node">
<a name="Command-or-script-execution"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Kill-process">Kill process</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Hashing-for-change-detection-_002d-tripwire">Hashing for change detection - tripwire</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.9 Command or script execution</h3>

<p>Execute a command, for instance to start a MySQL service. Note that executing commands takes control away from CFEngine and may disrupt the convergent nature of well written policies.

<pre class="verbatim">

body common control
{
bundlesequence  => { "my_commands" };
inputs => { "cfengine_stdlib.cf" };
}


bundle agent my_commands
{
commands:

 Sunday.Hr04.Min05_10.myhost::

  "/usr/bin/update_db";

 any::

  "/etc/mysql/start"

      contain => setuid("mysql");

}


</pre>

<!-- ...................................... -->
<div class="node">
<a name="Kill-process"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Restart-process">Restart process</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Command-or-script-execution">Command or script execution</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.10 Kill process</h3>

<pre class="verbatim">
body common control
{
bundlesequence => { "test" };
}



bundle agent test
{
processes:

 "sleep"

   signals => { "term", "kill" };

}
</pre>

<!-- ...................................... -->
<div class="node">
<a name="Restart-process"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Check-filesystem-space">Check filesystem space</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Kill-process">Kill process</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.11 Restart process</h3>

<p>A basic pattern for restarting processes:
<pre class="verbatim"></pre>
This can be made more sophisticated to handle generic lists:
<pre class="verbatim"></pre>

   <p>Why? Separating this into two parts gives a high level of control
and conistency to CFEngine. There are many options for command execution, like
the ability to run commands in a sandbox or as `setuid'. These should not be
reproduced in <code>processes</code>.

<!-- ...................................... -->
<div class="node">
<a name="Check-filesystem-space"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Mount-a-filesystem">Mount a filesystem</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Restart-process">Restart process</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.12 Check filesystem space</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "example" };
}

###########################################################

bundle agent example

{     
vars:

  "free" int => diskfree("/tmp"); 

reports:

  cfengine_3::

    "Freedisk $(free)";

}

</pre>

<!-- ...................................... -->
<div class="node">
<a name="Mount-a-filesystem"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Software-and-patch-installation">Software and patch installation</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Check-filesystem-space">Check filesystem space</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.13 Mount a filesystem</h3>

<pre class="verbatim">

#
# cfengine 3
#
# cf-agent -f ./cftest.cf -K
#

body common control

{
bundlesequence => { "mounts" };
}

#

bundle agent mounts

{
storage:

  "/mnt" mount  => nfs("slogans.iu.hio.no","/home");

}

######################################################################

body mount nfs(server,source)

{
mount_type => "nfs";
mount_source => "$(source)";
mount_server => "$(server)";
#mount_options => { "rw" };
edit_fstab => "true";
unmount => "true";
}
</pre>

<!-- ...................................... -->
<div class="node">
<a name="Software-and-patch-installation"></a>
<p><hr>
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Mount-a-filesystem">Mount a filesystem</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Introduction">Introduction</a>

</div>

<h3 class="section">1.14 Software and patch installation</h3>

<p>Example for Debian:
<pre class="verbatim">
# to see list of packages type "apt-cache pkgnames"
# to see list of installed packages type "dpkg --get-selections"
#
# Package managment
#

body common control
{
bundlesequence => { "packages" };
}

body agent control
{
environment => { "DEBIAN_FRONTEND=noninteractive" };
}

#############################################

bundle agent packages
{
vars:

 # Test the simplest case -- leave everything to the yum smart manager

 "match_package" slist => { 
                          "apache2" 
#                          "apache2-mod_php5",
#                          "apache2-prefork",
#                          "php5" 
                          };
packages:

  "$(match_package)"

     package_policy => "add",
     package_method => apt;

}

#############################################

body package_method apt

{
any::

# ii  acpi      0.09-3ubuntu1     

 package_changes => "bulk";
 package_list_command => "/usr/bin/dpkg -l";

 package_list_name_regex    => "ii\s+([^\s]+).*";
 package_list_version_regex => "ii\s+[^\s]+\s+([^\s]+).*";

# package_list_arch_regex    => "none";

 package_installed_regex => ".*"; # all reported are installed

 #package_name_convention => "$(name)_$(version)_$(arch)";
 package_name_convention => "$(name)";

 # Use these only if not using a separate version/arch string
 # package_version_regex => "";
 # package_name_regex => "";
 # package_arch_regex => "";

package_add_command => "/usr/bin/apt-get --yes install";
package_delete_command => "/usr/bin/apt-get --yes remove";
package_update_command =>  "/usr/bin/apt-get --yes dist-upgrade";
#package_verify_command => "/bin/rpm -V";
}

</pre>
Examples MSI for Windows, by name:
<pre class="verbatim"></pre>
Windows MSI by version:
<pre class="verbatim"></pre>
Examples for solaris are more complex:
<pre class="verbatim">

#
# Package managment
#

body common control
{
bundlesequence => { "packages" };
inputs => { "cfengine_stdlb.cf" };
}

#############################################

bundle agent packages
{
vars:

  "solaris_packages[SMCzlib]" string => "zlib-1.2.3-sol10-sparc-local";
  "admin_file"                string => "cfengine_admin_file";

  "package_names"              slist => getindices("solaris_packages");

files:

  "/tmp/$(admin_file)"
	create => "true",
	edit_defaults => empty_file,
	edit_line => create_solaris_admin_file;

packages:

  "$(package_names)"

     package_policy => "add",
     package_method => solaris("$(package_names)", "$(solaris_packages[$(package_names)])", "$(admin_file)");

}

</pre>
Examples for yum based systems:
<pre class="verbatim">

#
# Package managment
#

body common control
{
bundlesequence => { "packages" };
inputs => { "cfengine_stdlib.cf" }
}

#############################################

bundle agent packages
{
vars:

 # Test the simplest case -- leave everything to the yum smart manager

 "match_package" slist => { 
                          "apache2", 
                          "apache2-mod_php5",
                          "apache2-prefork",
                          "php5" 
                          };
packages:

  "$(match_package)"

     package_policy => "add",
     package_method => yum;

}

</pre>
SuSE Linux's package manager zypper is the most powerful alternative:
<pre class="verbatim">

#
# Package managment
#

body common control
{
bundlesequence => { "packages" };
inputs => { "cfengine_stdlib.cf" }
}

#############################################

bundle agent packages
{
vars:

 # Test the simplest case -- leave everything to the zypper smart manager

 "match_package" slist => { 
                          "apache2", 
                          "apache2-mod_php5",
                          "apache2-prefork",
                          "php5" 
                          };
packages:

  "$(match_package)"

     package_policy => "add",
     package_method => zypper;

}

</pre>

<!-- ********************************************************************** -->
<!-- CHAPTER -->
<!-- ********************************************************************** -->
<div class="node">
<a name="High-level"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Low-level">Low level</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Introduction">Introduction</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Top">Top</a>

</div>

<h2 class="chapter">2 High level</h2>

<!--  -->
<ul class="menu">
<li><a accesskey="1" href="#Centralized-Management">Centralized Management</a>
<li><a accesskey="2" href="#All-hosts-the-same">All hosts the same</a>
<li><a accesskey="3" href="#Variation-in-hosts">Variation in hosts</a>
<li><a accesskey="4" href="#Updating-from-a-central-hub">Updating from a central hub</a>
<li><a accesskey="5" href="#Change-detection">Change detection</a>
<li><a accesskey="6" href="#Garbage-collection">Garbage collection</a>
<li><a accesskey="7" href="#Distribute-root-passwords">Distribute root passwords</a>
<li><a accesskey="8" href="#Distribute-ssh-keys">Distribute ssh keys</a>
<li><a accesskey="9" href="#Laptop-support-configuration">Laptop support configuration</a>
<li><a href="#Find-MAC-address">Find MAC address</a>
<li><a href="#Log-rotation">Log rotation</a>
<li><a href="#Manage-a-system-file">Manage a system file</a>
<li><a href="#Simple-template">Simple template</a>
<li><a href="#Simple-versioned-template">Simple versioned template</a>
<li><a href="#Macro-template">Macro template</a>
<li><a href="#Custom-editing">Custom editing</a>
<li><a href="#Manage-a-system-process">Manage a system process</a>
<li><a href="#Ensure-running">Ensure running</a>
<li><a href="#Ensure-not-running">Ensure not running</a>
<li><a href="#Prune-processes">Prune processes</a>
<li><a href="#Manage-users">Manage users</a>
<li><a href="#Add-users">Add users</a>
<li><a href="#Remove-users">Remove users</a>
<li><a href="#Postfix-mail-configuration">Postfix mail configuration</a>
<li><a href="#Set-up-HPC-clusters">Set up HPC clusters</a>
<li><a href="#Set-up-name-resolution">Set up name resolution</a>
<li><a href="#Set-up-sudo">Set up sudo</a>
<li><a href="#Set-up-a-web-server">Set up a web server</a>
<li><a href="#Templating">Templating</a>
</ul>

<div class="node">
<a name="Centralized-Management"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#All-hosts-the-same">All hosts the same</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#High-level">High level</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.1 Centralized Management</h3>

<p>These examples show a simple setup for starting with a central approach to management of
servers. Centralization of management is a simple approach suitable for small environments
with few requirements. It is useful for clusters where systems are all alike.

<!--  -->
<ul class="menu">
<li><a accesskey="1" href="#All-hosts-the-same">All hosts the same</a>
<li><a accesskey="2" href="#Variation-in-hosts">Variation in hosts</a>
<li><a accesskey="3" href="#Updating-from-a-central-hub">Updating from a central hub</a>
</ul>

<div class="node">
<a name="All-hosts-the-same"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Variation-in-hosts">Variation in hosts</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Centralized-Management">Centralized Management</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.2 All hosts the same</h3>

<p>This shows the simplest approach in which all hosts are the same. It is too simple
for most environments, but it serves as a starting point. Compare it to the next
section that includes variation.

<pre class="verbatim">
body common control
   {
   bundlesequence  => { "central" };
   }


############################################

bundle agent central

{
vars:

  "policy_server" string => "myhost.domain.tld";

  "mypackages" slist => { 
                        "nagios"
                        "gcc",
                        "apache2", 
                        "php5" 
                        };

files:

  # Password management can be very simple if all hosts are identical

  "/etc/passwd" 

    comment   => "Distribute a password file",
    perms     => mog("644","root","root"),
    copy_from => secure_cp("/home/mark/LapTop/words/RoadAhead","$(policy_server)");

packages:

  "$(mypackages)"

     package_policy => "add",
     package_method => generic;


# Add more promises below ...
     
}


#########################################################
# Server config
#########################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "::1", "10.20.30" };
allowallconnects      => { "127.0.0.1" , "::1", "10.20.30" };
trustkeysfrom         => { "127.0.0.1" , "::1", "10.20.30" };
# allowusers
}

#########################################################

bundle server access_rules()

{
access:

 # myhost.domain.tld makes this file available to 10.20.30*

 myhost_domain_tld::  

  "/etc/passwd"

     admit   => { "127.0.0.1", "10.20.30" };
}

</pre>

<!--  -->
<div class="node">
<a name="Variation-in-hosts"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Updating-from-a-central-hub">Updating from a central hub</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#All-hosts-the-same">All hosts the same</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.3 Variation in hosts</h3>

<pre class="verbatim">
body common control
   {
   bundlesequence  => { "central" };
   }


############################################

bundle agent central

{
classes:

  "mygroup_1" or => { "myhost", "host1", "host2", "host3" };
  "mygroup_2" or => { "host4", "host5", "host6" };

vars:

  "policy_server" string => "myhost.domain.tld";

 mygroup_1::

  "mypackages" slist => { 
                        "nagios"
                        "gcc",
                        "apache2", 
                        "php5" 
                        };

 mygroup_2::

  "mypackages" slist => { 
                        "apache"
                        "mysql",
                        "php5" 
                        };


files:

  # Password management can be very simple if all hosts are identical

  "/etc/passwd" 

    comment   => "Distribute a password file",
    perms     => mog("644","root","root"),
    copy_from => secure_cp("/etc/passwd","$(policy_server)");

packages:

  "$(mypackages)"

     package_policy => "add",
     package_method => generic;


# Add more promises below ...
     
}


#########################################################
# Server config
#########################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "::1", "10.20.30" };
allowallconnects      => { "127.0.0.1" , "::1", "10.20.30" };
trustkeysfrom         => { "127.0.0.1" , "::1", "10.20.30" };
# allowusers
}

#########################################################

bundle server access_rules()

{
access:

 # myhost.domain.tld makes this file available to 10.20.30*

 myhost_domain_tld::  

  "/etc/passwd"

     admit   => { "127.0.0.1", "10.20.30" };
}

</pre>

<!--  -->
<div class="node">
<a name="Updating-from-a-central-hub"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Change-detection">Change detection</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Variation-in-hosts">Variation in hosts</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.4 Updating from a central hub</h3>

<p>The configuration bundled with the CFEngine source code contains an
example of centralized updating of policy that covers more subtleties
than this example, and handles fault tolerance.  Here is the main idea
behind it. For simplicity, we assume that all hosts are on network
10.20.30.* and that the central policy server/hub is 10.20.30.123.

<pre class="verbatim">
bundle agent update
{
vars:

 "master_location" string => "/var/cfengine/masterfiles";

 "policy_server"   string => "10.20.30.123";
                   comment => "IP address to locate your policy host.";

files:

  "$(sys.workdir)/inputs" 

    perms => system("600"),
    copy_from => remote_cp("$(master_location)",$(policy_server)),
    depth_search => recurse("inf");

  "$(sys.workdir)/bin" 

    perms => system("700"),
    copy_from => remote_cp("/usr/local/sbin","localhost"),
    depth_search => recurse("inf");
}

#######################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "10.20.30" };
allowallconnects      => { "127.0.0.1" , "10.20.30" };
trustkeysfrom         => { "127.0.0.1" , "10.20.30" };
}

#######################################################

bundle server access_rules()
{
access:

 10_20_30_123::

  "/var/cfengine/masterfiles"

    admit   => { "127.0.0.1", "10.20.30" };
}


</pre>

<!--  -->
<div class="node">
<a name="Change-detection"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Garbage-collection">Garbage collection</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Updating-from-a-central-hub">Updating from a central hub</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.5 Change detection</h3>

<pre class="verbatim">
body common control

{
bundlesequence  => { "testbundle"  };

inputs => { "cfengine_stdlib.cf" };
}

########################################################

bundle agent testbundle

{
files:

  "/usr" 

       changes      => detect_all_change,
       depth_search => recurse("inf"),
       action       => background;
}

</pre>

<!--  -->
<div class="node">
<a name="Garbage-collection"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Distribute-root-passwords">Distribute root passwords</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Change-detection">Change detection</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.6 Garbage collection</h3>

<pre class="verbatim">
body common control
{
bundlesequence => { "garbage_collection" };
inputs => { "cfengine_stdlib.cf" };
}


bundle agent garbage_collection
{
files:

 Sunday::

  "$(sys.workdir)/nova_repair.log" 

    comment => "Rotate the promises repaired logs each week",
    rename => rotate("7"),
    action => if_elapsed("10000");

  "$(sys.workdir)/nova_notkept.log" 

    comment => "Rotate the promises not kept logs each week",
    rename => rotate("7"),
    action => if_elapsed("10000");

  "$(sys.workdir)/promise.log" 

    comment => "Rotate the promises not kept logs each week",
    rename => rotate("7"),
    action => if_elapsed("10000");

 any::

  "$(sys.workdir)/outputs" 

    comment => "Garbage collection of any output files",
    delete => tidy,
    file_select => days_old("3"),
    depth_search => recurse("inf");

  "$(sys.workdir)/" 

    comment => "Garbage collection of any output files",
    delete => tidy,
    file_select => days_old("14"),
    depth_search => recurse("inf");

  # Other resources

  "/tmp" 

    comment => "Garbage collection of any temporary files",
    delete => tidy,
    file_select => days_old("3"),
    depth_search => recurse("inf");
  
  "/var/log/apache2/.*bz" 

    comment => "Garbage collection of rotated log files",
    delete => tidy,
    file_select => days_old("30"),
    depth_search => recurse("inf");

  "/var/log/apache2/.*gz" 

    comment => "Garbage collection of rotated log files",
    delete => tidy,
    file_select => days_old("30"),
    depth_search => recurse("inf");

  "/var/log/zypper.log"

    comment => "Prevent the zypper log from choking the disk",
    rename => rotate("0"),
    action => if_elapsed("10000");

}


</pre>

<!--  -->
<div class="node">
<a name="Distribute-root-passwords"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Distribute-ssh-keys">Distribute ssh keys</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Garbage-collection">Garbage collection</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.7 Distribute root passwords</h3>

<pre class="verbatim">######################################################################
#
# Root password distribution
# 
######################################################################

body common control

{
version => "1.2.3";
bundlesequence  => { "SetRootPassword" };
}

########################################################

bundle common g
{
vars:

  "secret_keys_dir" string => "/tmp";
}

########################################################

bundle agent SetRootPassword

{
files:

  "/var/cfengine/ppkeys/rootpw.txt"

      copy_from => scp("$(sys.fqhost)-root.txt","master_host.example.org");

      # or $(pw_class)-root.txt

  # Or get variables directly from server woth Nova

 "remote-passwd" string => remotescalar("rem_password","127.0.0.1","yes");

  # Test this on a copy

  "/tmp/shadow"

       edit_line => SetRootPw;

}

########################################################

bundle edit_line SetRootPw
  {
  vars:

   # Assume this file contains a single string of the form root:passwdhash:
   # with : delimiters to avoid end of line/file problems

   "pw" int => readstringarray("rpw","$(sys.workdir)/ppkeys/rootpw.txt",
                                                    "#[^\n]*",":","1","200");

  field_edits:

   "root:.*"

      # Set field of the file to parameter

      edit_field => col(":","2","$(rpw[1])","set");
  }

########################################################

bundle server passwords
{
vars:

  # Read a file of format
  #
  # classname: host1,host2,host4,IP-address,regex.*,etc
  #

       "pw_classes" int => readstringarray("acl","$(g.secret_keys_dir)/classes.txt",
                                                       "#[^\n]*",":","100","4000");  
  "each_pw_class" slist => getindices("acl");
 
access:

  "/secret/keys/$(each_pw_class)-root.txt"

        admit   => splitstring("$(acl[$(each_pw_class)][1])" , ":" , "100"),
    ifencrypted => "true";

}

</pre>

<!--  -->
<div class="node">
<a name="Distribute-ssh-keys"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Laptop-support-configuration">Laptop support configuration</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Distribute-root-passwords">Distribute root passwords</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.8 Distribute ssh keys</h3>

<pre class="verbatim"># Assume that we have collected all users' public keys into a single source area
# on the server. First copy the ones we need to localhost, and then edit them into
# the the user's local keyring.

  # vars:
  #
  #  "users" slist => { "user1", "user2", ...};
  #
  # methods:
  #
  #  "any" usebundle => allow_ssh_login_from_authorized_keys(@(users),"sourcehost");
  #

########################################################################

bundle agent allow_ssh_rootlogin_from_authorized_keys(user,sourcehost)
{
vars:

  "local_cache"       string => "/var/cfengine/ssh_cache"; 
  "authorized_source" string => "/master/CFEngine/ssh_keys";

files:

   "$(local_cache)/$(user).pub"

         comment => "Copy public keys from a an authorized cache into a cache on localhost",
           perms => mo("600","root"),
       copy_from => remote_cp("$(authorized_source)/$(user).pub","$(sourcehost)"),
          action => if_elapsed("60");

   "/root/.ssh/authorized_keys" 

         comment => "Edit the authorized keys into the user's personal keyring",
       edit_line => insert_file_if_no_line_matching("$(user)","$(local_cache)/$(user).pub"),
          action => if_elapsed("60");
}

########################################################################

bundle agent allow_ssh_login_from_authorized_keys(user,sourcehost)
{
vars:

  "local_cache"       string => "/var/cfengine/ssh_cache"; 
  "authorized_source" string => "/master/CFEngine/ssh_keys";

files:

   "$(local_cache)/$(user).pub"

         comment => "Copy public keys from a an authorized cache into a cache on localhost",
           perms => mo("600","root"),
       copy_from => remote_cp("$(authorized_source)/$(user).pub","$(sourcehost)"),
          action => if_elapsed("60");

   "/home/$(user)/.ssh/authorized_keys" 

         comment => "Edit the authorized keys into the user's personal keyring",
       edit_line => insert_file_if_no_line_matching("$(user)","$(local_cache)/$(user).pub"),
          action => if_elapsed("60");
}

########################################################################

bundle edit_line insert_file_if_no_line_matching(user,file)
{
classes:

  "have_user" expression => regline("$(user).*","$(this.promiser)");

insert_lines:

  !have_user::

    "$(file)" 
         insert_type => "file";
}

</pre>

<!--  -->
<div class="node">
<a name="Laptop-support-configuration"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Find-MAC-address">Find MAC address</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Distribute-ssh-keys">Distribute ssh keys</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.9 Laptop support configuration</h3>

<p>Laptops do not need a lot of confguration support. IP addresses are
set by DHCP and conditions are changeable. But you want to set your
DNS search domains to familiar settings in spite of local DHCP
configuration, and another useful trick is to keep a regular backup of
disk changes on the local disk. This won't help against disk
destruction, but it is a huge advantage when your user accidentally
deletes files while travelling or offline.

<pre class="verbatim">#######################################################
#
# Laptop
#
#######################################################

body common control

{
bundlesequence  => { 
                   "update",
                   "garbage_collection",
                   "main",
                   "backup",
                   };

inputs          => {
                   "update.cf",
                   "site.cf",
                   "library.cf" 
                   };
}

#######################################################

body agent control
{
# if default runtime is 5 mins we need this for long jobs
ifelapsed => "15";
}

#######################################################

body monitor control
{
forgetrate => "0.7";
}

#######################################################

body executor control

{
splaytime => "1";
mailto => "mark@iu.hio.no";
smtpserver => "localhost";
mailmaxlines => "30";

# Instead of a separate update script, now do this

exec_command => "$(sys.workdir)/bin/cf-agent -f failsafe.cf &amp;&amp; $(sys.workdir)/bin/cf-agent";
}

#######################################################
# General site issues can be in bundles like this one
#######################################################

bundle agent main

{
vars:

  "component" slist => { "cf-monitord", "cf-serverd" };

 # - - - - - - - - - - - - - - - - - - - - - - - -

files:

  "$(sys.resolv)"  # test on "/tmp/resolv.conf" #

     create        => "true",
     edit_line     => resolver,
     edit_defaults => def;

processes:

  "$(component)" restart_class => canonify("start_$(component)");

 # - - - - - - - - - - - - - - - - - - - - - - - -

commands:

   "$(sys.workdir)/bin/$(component)"

       ifvarclass => canonify("start_$(component)");
}

#######################################################
# Backup
#######################################################

bundle agent backup
{
files:

  "/home/backup"

     copy_from => cp("/home/mark"),
  depth_search => recurse("inf"),
   file_select => exclude_files,
        action => longjob;

}

#######################################################
# Garbage collection issues
#######################################################

bundle agent garbage_collection
{
files:

  "$(sys.workdir)/outputs" 

    delete => tidy,
    file_select => days_old("3"),
    depth_search => recurse("inf");


}

</pre>

<!--  -->
<div class="node">
<a name="Find-MAC-address"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Log-rotation">Log rotation</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Laptop-support-configuration">Laptop support configuration</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.10 Find the MAC address</h3>

<p>Finding the ethernet address can be hard, but on Linux it is straightforward.

<pre class="verbatim">bundle agent test
{
vars:

linux::
 "interface" string => execresult("/sbin/ifconfig eth0","noshell");

solaris::
 "interface" string => execresult("/usr/sbin/ifconfig bge0","noshell");

freebsd::
 "interface" string => execresult("/sbin/ifconfig le0","noshell");

darwin::
 "interface" string => execresult("/sbin/ifconfig en0","noshell");

classes:

 linux::

   "ok" expression => regextract(
                                ".*HWaddr ([^\s]+).*(\n.*)*",
                                "$(interface)",
                                "mac"
                                );

 solaris::

   "ok" expression => regextract(
                                ".*ether ([^\s]+).*(\n.*)*",
                                "$(interface)",
                                "mac"
                                );

 freebsd::

   "ok" expression => regextract(
                                ".*ether ([^\s]+).*(\n.*)*",
                                "$(interface)",
                                "mac"
                                );

 darwin::

   "ok" expression => regextract(
                                "(?s).*ether ([^\s]+).*(\n.*)*",
                                "$(interface)",
                                "mac"
                                );

reports:

ok::

  "MAC address is $(mac[1])";

}

</pre>

<!--  -->
<div class="node">
<a name="Log-rotation"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Manage-a-system-file">Manage a system file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Find-MAC-address">Find MAC address</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.11 Log rotation</h3>

<pre class="verbatim">
body common control
   {
   bundlesequence  => { "testbundle" };
   }


############################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/rotateme"

      rename => rotate("4");
}

############################################

body rename rotate(level)

{
rotate => "$(level)";
}

</pre>

<!--  -->
<div class="node">
<a name="Manage-a-system-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Simple-template">Simple template</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Log-rotation">Log rotation</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.12 Manage a system file</h3>

<ul class="menu">
<li><a accesskey="1" href="#Simple-template">Simple template</a>
<li><a accesskey="2" href="#Simple-versioned-template">Simple versioned template</a>
<li><a accesskey="3" href="#Macro-template">Macro template</a>
<li><a accesskey="4" href="#Custom-editing">Custom editing</a>
</ul>

<div class="node">
<a name="Simple-template"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Simple-versioned-template">Simple versioned template</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Manage-a-system-file">Manage a system file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.13 Simple template</h3>

<pre class="verbatim">bundle agent hand_edited_config_file
{
vars:

  "file_template" string =>

"
# Syntax:
#
# IP-Address  Full-Qualified-Hostname  Short-Hostname
#

127.0.0.1       localhost
::1             localhost ipv6-localhost ipv6-loopback
fe00::0         ipv6-localnet
ff00::0         ipv6-mcastprefix
ff02::1         ipv6-allnodes
ff02::2         ipv6-allrouters
ff02::3         ipv6-allhosts
10.0.0.100      host1.domain.tld host1
10.0.0.101      host2.domain.tld host2
10.0.0.20       host3.domain.tld host3
10.0.0.21       host4.domain.tld host4
";

##############################################################

files:

   "/etc/hosts"

       comment => "Define the content of all host files from this master source",
        create => "true",
     edit_line => append_if_no_lines("$(file_template)"),
 edit_defaults => empty,
         perms => mo("$(mode)","root"),
        action => if_elapsed("60");
}

</pre>

<div class="node">
<a name="Simple-versioned-template"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Macro-template">Macro template</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Simple-template">Simple template</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.14 Simple versioned template</h3>

<p>The simplest approach to managing a file is to maintain a master copy by hand, keeping it
in a version controlled repository (e.g. svn), and installing this version on the end machine.

   <p>We'll assume that you have a version control repository that is located on some independent server, and has been
checked out manually once (with authentication) in <samp><span class="file">/mysite/masterfiles</span></samp>.

<pre class="verbatim">bundle agent hand_edited_config_file
{
vars:

  "masterfiles"   string => "/mysite/masterfiles";
  "policy_server" string => "policy_host.domain.tld";

files:

   "/etc/hosts"

        comment => "Synchronize hosts with a hand-edited template in svn",
          perms => m("644"),
      copy_from => remote_cp("$(masterfiles)/trunk/hosts_master","$(policy_server)");

commands:


   "/usr/bin/svn update" 

        comment => "Update the company document repository including manuals to a local copy",
        contain => silent_in_dir("$(masterfiles)/trunk"),
     ifvarclass => canonify("$(policy_server)");

}

</pre>

<div class="node">
<a name="Macro-template"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Custom-editing">Custom editing</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Simple-versioned-template">Simple versioned template</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.15 Macro template</h3>

<p>The next simplest approach to file management is to add variables to
the template that will be expanded into local values at the end
system, e.g. using variables like &lsquo;<samp><span class="samp">$(sys.host)</span></samp>&rsquo; for the name of
the host within the body of the versioned template.

<pre class="verbatim">bundle agent hand_edited_template
{
vars:

  "masterfiles"   string => "/mysite/masterfiles";
  "policy_server" string => "policy_host.domain.tld";

files:

   "/etc/hosts"

        comment => "Synchronize hosts with a hand-edited template in svn",
          perms => m("644"),
        create => "true",
     edit_line => expand_template("$(masterfiles)/trunk/hosts_master"),
 edit_defaults => empty,
        action => if_elapsed("60");

commands:


   "/usr/bin/svn update" 

        comment => "Update the company document repository including manuals to a local copy",
        contain => silent_in_dir("$(masterfiles)/trunk"),
     ifvarclass => canonify("$(policy_server)");

}

</pre>

<p class="noindent">The macro template file may contain variables, as below,
that get expanded by CFEngine.

<pre class="smallexample">     # Syntax:
     #
     # IP-Address  Full-Qualified-Hostname  Short-Hostname
     #
     
     127.0.0.1       localhost $(sys.host)
     ::1             localhost ipv6-localhost ipv6-loopback
     fe00::0         ipv6-localnet
     ff00::0         ipv6-mcastprefix
     ff02::1         ipv6-allnodes
     ff02::2         ipv6-allrouters
     ff02::3         ipv6-allhosts
     10.0.0.100      host1.domain.tld host1
     10.0.0.101      host2.domain.tld host2
     10.0.0.20       host3.domain.tld host3
     10.0.0.21       host4.domain.tld host4
     
     # Add below this line
     
     $(definitions.more_hosts)
</pre>
   <div class="node">
<a name="Custom-editing"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Manage-a-system-process">Manage a system process</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Macro-template">Macro template</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.16 Custom editing</h3>

<p>If you do not control the starting state of the file, because it is distributed by
an operating system vendor for instance, then editing the final state is the best approach. 
That way, you will get changes that are made by the vendor, and will ensure your own
modifications are kept even when updates arrive.

<pre class="verbatim">bundle agent modifying_managed_file
{
vars:

  "data"   slist => { "10.1.2.3 sirius", "10.1.2.4 ursa-minor", "10.1.2.5 orion"};

files:

   "/etc/hosts"

        comment => "Append a list of lines to the end of a file if they don't exist",
          perms => m("644"),
        create => "true",
     edit_line => append_if_no_lines("modifying_managed_file.data"),
        action => if_elapsed("60");


}

</pre>

   <p>Another example shows how to set the values of variables
using a data-driven approach and methods from the
standard library.
<pre class="verbatim">#######################################################
#
# Edit variable = value in a text file
#
#######################################################

body common control

{
bundlesequence  => { "testsetvar" };   
}

#######################################################

bundle agent testsetvar

{
vars:

  "v[variable_1]" string => "value_1";
  "v[variable_2]" string => "value_2";

files:

  "/tmp/test_setvar"

     edit_line => set_variable_values("testsetvar.v");
}

</pre>

<!--  -->
<div class="node">
<a name="Manage-a-system-process"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Ensure-running">Ensure running</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Custom-editing">Custom editing</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.17 Manage a system process</h3>

<ul class="menu">
<li><a accesskey="1" href="#Ensure-running">Ensure running</a>
<li><a accesskey="2" href="#Ensure-not-running">Ensure not running</a>
<li><a accesskey="3" href="#Prune-processes">Prune processes</a>
</ul>

<div class="node">
<a name="Ensure-running"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Ensure-not-running">Ensure not running</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Manage-a-system-process">Manage a system process</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.18 Ensure running</h3>

<p>The simplest example might look like this:

<pre class="verbatim">
bundle agent restart_process
{
processes:

  "httpd" 

      comment => "Make sure apache web server is running",
      restart_class => "restart_httpd";

commands:

 restart_httpd::

     "/etc/init.d/apache2 restart";

}

</pre>

   <p>This example shows how the CFEngine components could be started using a pattern.
<pre class="verbatim">
bundle agent CFEngine_processes
{
vars:

  "components" slist => { "cf-execd", "cf-monitord", "cf-serverd", "cf-hub" };

processes:

  "$(components)" 

      comment => "Make sure server parts of CFEngine are running",
      restart_class => canonify("start_$(component)");

commands:

   "$(sys.workdir)/bin/$(component)"

          comment => "Make sure server parts of CFEngine are running",
       ifvarclass => canonify("start_$(components)");

}

</pre>

<div class="node">
<a name="Ensure-not-running"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Prune-processes">Prune processes</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Ensure-running">Ensure running</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.19 Ensure not running</h3>

<pre class="verbatim">
bundle agent restart_process
{
vars:

  "killprocs" slist => { "snmpd", "gameserverd", "irc", "crack" };

processes:

  "$(killprocs)" 

      comment => "Make processes are not running",
      signals => { "term", "kill" };
;
}

</pre>

<div class="node">
<a name="Prune-processes"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Manage-users">Manage users</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Ensure-not-running">Ensure not running</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.20 Prune processes</h3>

<p>This example kills processes owned by a particular user that have exceeded 100000 bytes of
resident memory.

<pre class="verbatim">
body common control
{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
processes:

 ".*"

    process_select  => big_processes("mark"),
            signals => { "term" };
}

########################################################

body process_select big_processes(o)

{
process_owner => { "$(o)" };
rsize => irange("100000","900000");
process_result => "rsize.process_owner";
}

</pre>

<!--  -->
<div class="node">
<a name="Manage-users"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Add-users">Add users</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Prune-processes">Prune processes</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.21 Manage users</h3>

<p>There are many approaches to managing users. You can edit system files like <samp><span class="file">/etc/passwd</span></samp>
directly, or you can use commands on some systems like &lsquo;<samp><span class="samp">useradd</span></samp>&rsquo; or &lsquo;<samp><span class="samp">adduser</span></samp>&rsquo;. 
In all cases it is desirable to make this a data-driven process.

<ul class="menu">
<li><a accesskey="1" href="#Add-users">Add users</a>
<li><a accesskey="2" href="#Remove-users">Remove users</a>
</ul>

<div class="node">
<a name="Add-users"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Remove-users">Remove users</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Manage-users">Manage users</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.22 Add users</h3>

<p>A simple approach which adds new users to the password file, and to a
group called &lsquo;<samp><span class="samp">users</span></samp>&rsquo; in the group file. Is shown below. This example
does not edit the shadow file. A simple pattern that can be modified for
use is shown below.

   <p>Note that, although this is a simple minded approach, it is the most efficient
of the approaches shown here as all operations can be carried out in a single
operation for each file.
<pre class="verbatim">bundle agent addusers
{
vars:

  # Add some users

  "pw[mark]" string => "mark:x:1000:100:Mark Burgess:/home/mark:/bin/bash";
  "pw[fred]" string => "fred:x:1001:100:Right Said:/home/fred:/bin/bash";
  "pw[jane]" string => "jane:x:1002:100:Jane Doe:/home/jane:/bin/bash";

  "users" slist => getindices("pw");

files:

  "/etc/passwd"
     edit_line => append_users_starting("addusers.pw");

#  "/etc/shadow"
#     edit_line => append_users_starting("$(users):defaultpasswd:::::::");

  "/etc/group"
       edit_line => append_user_field("users","4","@(addusers.users)");

  "/home/$(users)/."

     create => "true",
      perms => mog("755","$(users)","users");
}

</pre>

   <p>A second approach is to use the shell commands supplied by some operating systems;
this assumes that suitable defaults have been set up manually. Also the result
is not repairable in a simple convergent manner. The command needs to edit multiple
files for each user, and is quite inefficient.

<pre class="verbatim">bundle agent addusers
{
vars:

  "users" slist => { "mark", "fred", "jane" };

commands:

   "/usr/sbin/useradd $(users)";
}

</pre>
An alternative approach is to use a method to wrap around the handling of a user. 
Although this looks nice, it is less efficient than the first method because it
must edit the files multiple times.
<pre class="verbatim">bundle agent addusers
{
vars:

  # Add some users

  "pw[mark]" string => "mark:x:1000:100:Mark Burgess:/home/mark:/bin/bash";
  "pw[fred]" string => "fred:x:1001:100:Right Said:/home/fred:/bin/bash";
  "pw[jane]" string => "jane:x:1002:100:Jane Doe:/home/jane:/bin/bash";

  "users" slist => getindices("pw");

methods:

  "any" usebundle => user_add("$(users)","$(pw[$(users)])");

}

bundle agent user_add(x,pw)
{
files:

  "/etc/passwd"
     edit_line => append_users_starting("addusers.pw");

#  "/etc/shadow"
#     edit_line => append_users_starting("$(users):defaultpasswd:::::::");

  "/etc/group"
       edit_line => append_user_field("users","4","@(addusers.users)");

  "/home/$(users)/."

     create => "true",
      perms => mog("755","$(users)","users");
}

</pre>

<div class="node">
<a name="Remove-users"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Postfix-mail-configuration">Postfix mail configuration</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Add-users">Add users</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.23 Remove users</h3>

<!--  -->
<div class="node">
<a name="Postfix-mail-configuration"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Set-up-HPC-clusters">Set up HPC clusters</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Remove-users">Remove users</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.24 Postfix mail configuration</h3>

<pre class="verbatim">#######################################################
#
# Postfix
#
#######################################################

body common control

{
any::

  bundlesequence  => {
                     postfix
                     };   
}

#######################################################

bundle agent postfix

{
vars:

 "prefix"     string => "/etc";
 "smtpserver" string => "localhost";
 "mailrelay"  string => "mailx.example.org";

files:

  "$(prefix)/main.cf"     
      edit_line => prefix_postfix;

  "$(prefix)/sasl-passwd" 
      create    => "true",
      perms     => mo("0600","root"),
      edit_line => append_if_no_line("$(smtpserver) _$(sys.fqhost):chmsxrcynz4etfrejizhs22");
}

#######################################################
# For the library
#######################################################

bundle edit_line prefix_postfix

{
#
# Value have the form NAME = "quoted space separated list"
#
vars:

  "ps[relayhost]"                  string => "[$(postfix.mailrelay)]:587";
  "ps[mydomain]"                   string => "iu.hio.no";
  "ps[smtp_sasl_auth_enable]"      string => "yes";
  "ps[smtp_sasl_password_maps]"    string => "hash:/etc/postfix/sasl-passwd";
  "ps[smtp_sasl_security_options]" string => "";
  "ps[smtp_use_tls]"               string => "yes";
  "ps[default_privs]"              string => "mailman";
  "ps[inet_protocols]"             string => "all";
  "ps[inet_interfaces]"            string => "127.0.0.1";

  "parameter_name" slist => getindices("ps");

delete_lines: 

  "$(parameter_name).*";

insert_lines:

  "$(parameter_name) = $(ps[$(parameter_name)])";

}

########################################################

bundle edit_line AppendIfNSL(parameter)
  {
  insert_lines:

    "$(parameter)"; # This is default
  }

</pre>

<div class="node">
<a name="Set-up-HPC-clusters"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Set-up-name-resolution">Set up name resolution</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Postfix-mail-configuration">Postfix mail configuration</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.25 Set up HPC clusters</h3>

<p>HPC cluster machines are usually all identical, so the CFEngine
configuration is very simple. HPC clients value CPU and memory
resources, so we can shut down unnecessary services to save CPU. 
We can also change the scheduling rate of CFEngine to run less frequently,
and save a little:

<pre class="verbatim">
#######################################################

body executor control

{
splaytime => "1";
mailto => "cfengine@example.com";
smtpserver => "localhost";
mailmaxlines => "30";

# Once per hour, on the hour

schedule     => { "Min00_05" };
}

#######################################################

bundle agent services_disable
{
vars:

   # list all of xinetd services (case sensitive)

   "xinetd_services" slist => {
                               "imap",
                               "imaps",
                               "ipop2",
                               "ipop3",
                               "krb5-telnet",
                               "klogin",
                               "kshell",
                               "ktalk",
                               "ntalk",
                               "pop3s",

                              };
methods:

   # perform the actual disable all xinetd services according to the list above

   "any"  usebundle => disable_xinetd("$(xinetd_services)");

processes:

  "$(xinetd_services)"

           signals => { "kill" };

}

###############################################################################

bundle agent disable_xinetd(name)
{
 vars:
   "status" string => execresult("/sbin/chkconfig --list $(name)", "useshell");

 classes:
   "on"  expression => regcmp(".*on.*","$(status)");
   
 commands:
   on::
      "/sbin/chkconfig $(name) off",
        comment => "disable $(name) service";

 reports:
   on::
      "disable $(name) service.";
   
}
</pre>

<!--  -->
<div class="node">
<a name="Set-up-name-resolution"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Set-up-sudo">Set up sudo</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Set-up-HPC-clusters">Set up HPC clusters</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.26 Set up name resolution</h3>

<p>There are many ways to do name resolution setup<a rel="footnote" href="#fn-1" name="fnd-1"><sup>1</sup></a> We write a reusable
bundle using the editing features.

   <p>A simple and straightforward approach is to maintain
a separate modular bundle for this task. This avoids
too many levels of abstraction and keeps all the information
in one place. We implement this as a simple editing promise
for the <samp><span class="file">/etc/resolv.conf</span></samp> file.
<pre class="verbatim">
bundle agent system_files

{
files:

  "$(sys.resolv)"  # test on "/tmp/resolv.conf" #

     comment       => "Add lines to the resolver configuration",
     create        => "true",
     edit_line     => resolver,
     edit_defaults => std_edits;

   # ...other system files ...

}

#######################################################

bundle edit_line resolver

{
delete_lines:

  # delete any old name servers or junk we no longer need

  "search.*";
  "nameserver 80.65.58.31";
  "nameserver 80.65.58.32";
  "nameserver 82.103.128.146";
  "nameserver 78.24.145.4";
  "nameserver 78.24.145.5";
  "nameserver 128.39.89.10";

insert_lines:

  "search mydomain.tld" location => start;

 special_net::

  "nameserver 128.39.89.8";
  "nameserver 128.39.74.66";

 !special_net::

  "nameserver 128.38.34.12";

 any::

  "nameserver 212.112.166.18";
  "nameserver 212.112.166.22";
}

</pre>

   <p>A second approach is to try to conceal the operational details
behind a veil of abstraction.

<pre class="verbatim">
bundle agent system_files
{
vars:

 "searchlist"  string => "iu.hio.no CFEngine.com";

 "nameservers" slist => { 
                        "128.39.89.10", 
                        "128.39.74.16",
                        "192.168.1.103"
                        };
files:

  "$(sys.resolv)"  # test on "/tmp/resolv.conf" #
      create        => "true",
      edit_line     => doresolv("$(s)","@(this.n)"),
      edit_defaults => empty;

 # ....

}

#######################################################

bundle edit_line doresolv(search,names)

{
insert_lines:
  "search $(search)";
  "nameserver $(names)";
}

</pre>

<p class="noindent">DNS is not the only name service, of course. 
Unix has its older <samp><span class="file">/etc/hosts</span></samp> file which can also
be managed using file editing. We simply append this to the
<code>system_files</code> bundle.

<pre class="verbatim">bundle agent system_files
{

# ...

files:

   "/etc/hosts"

     comment => "Add hosts to the /etc/hosts file",
   edit_line => fix_etc_hosts;
}

###########################################################

bundle edit_line fix_etc_hosts
{
vars:

 "names[127.0.0.1]"    string => "localhost localhost.CFEngine.com";
 "names[128.39.89.12]" string => "myhost myhost.CFEngine.com";
 "names[128.39.89.13]" string => "otherhost otherhost.CFEngine.com";

 # etc

 "i" slist => getindices("names");

insert_lines:

 "$(i)     $(names[$(i)])";

}

</pre>

<!--  -->
<div class="node">
<a name="Set-up-sudo"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Set-up-a-web-server">Set up a web server</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Set-up-name-resolution">Set up name resolution</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.27 Set up sudo</h3>

<p>Setting up <code>sudo</code> is straightforward, and is best managed by copying trusted
files from a repository.
<pre class="verbatim">bundle agent system_files
{
vars:

 "masterfiles" string => "/subversion_projects/masterfiles";

# ...

files:

   "/etc/sudoers"

     comment => "Make sure the sudo configuration is secure and up to date",
       perms => mog("440","root","root"),
   copy_from => secure_cp("$(masterfiles)/sudoers","$(policy_server)");

}

</pre>

<!--  -->
<div class="node">
<a name="Set-up-a-web-server"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Templating">Templating</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Set-up-sudo">Set up sudo</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.28 Set up a web server</h3>

<p>Adapt this template to your operating system by adding multiple
classes.  Each web server runs something like the present module,
which is entered into the bundlesequence like this:

<pre class="verbatim">#####################################################
#
# Apache webserver module
# 
#####################################################

bundle agent web_server(state)
{
vars:

  "document_root" string => "/";

 ####################################################
 # Site specific configuration - put it in this file
 ####################################################

  "site_http_conf" string => "/home/mark/CFEngine-inputs/httpd.conf";

 ####################################################
 # Software base
 ####################################################

  "match_package" slist => { 
                           "apache2", 
                           "apache2-mod_php5",
                           "apache2-prefork",
                           "php5" 
                           };

 #########################################################

processes:

  web_ok.on::

   "apache2"
 
     restart_class => "start_apache";

  off::

   "apache2"

     process_stop => "/etc/init.d/apache2 stop";


 #########################################################

commands:

 start_apache::

   "/etc/init.d/apache2 start"; # or startssl

 #########################################################

packages:

  "$(match_package)"

     package_policy => "add",
     package_method => zypper,
     classes => if_ok("software_ok");

 #########################################################

files:

 software_ok::

  "/etc/sysconfig/apache2" 

     edit_line => fixapache,
     classes => if_ok("web_ok");

 #########################################################

reports:

 !software_ok.on::

    "The web server software could not be installed";
 
 #########################################################

classes:

  "on"  expression => strcmp("$(state)","on");
  "off" expression => strcmp("$(state)","off");
}

#######################################################
# For the library
#######################################################

bundle edit_line fixapache

{
vars:

 "add_modules"     slist => { 
                            "ssl", 
                            "php5" 
                            };

 "del_modules"     slist => { 
                            "php3",
                            "php4",
                            "jk"
                            };

insert_lines:

 "APACHE_CONF_INCLUDE_FILES=\"$(web_server.site_http_conf)\"";

field_edits:

 #####################################################################
 # APACHE_MODULES="actions alias ssl php5 dav_svn authz_default jk" etc..
 #####################################################################

   "APACHE_MODULES=.*"

      # Insert module "columns" between the quoted RHS 
      # using space separators

      edit_field => quotedvar("$(add_modules)","append");

   "APACHE_MODULES=.*"

      # Delete module "columns" between the quoted RHS 
      # using space separators

      edit_field => quotedvar("$(del_modules)","delete");

   # if this line already exists, edit it  

}

</pre>

<!--  -->
<div class="node">
<a name="Templating"></a>
<p><hr>
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Set-up-a-web-server">Set up a web server</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#High-level">High level</a>

</div>

<h3 class="section">2.29 Templating</h3>

<p>With CFEngine you have a choice between editing `deltas' into files
or distributing more-or-less finished templates. Which method you
should choose depends should be made by whatever is easiest.

     <ul>
<li>If you are managing only part of the file, and something else (e.g. a package manager)
is managing most of it, then it makes sense to use CFEngine file editing.

     <li>If you are managing everything in the file, then it makes sense to make the edits
by hand and install them using CFEngine. You can use variables within
source text files and let CFEngine expand them locally in situ, so
that you can make generic templates that apply netwide.

   </ul>
   Example template:

<pre class="verbatim">#
# System file X
#

MYVARIABLE = something or other
HOSTNAME = $(sys.host)           # CFEngine fills this in

# ...

</pre>
To copy and expand this template, you can use a pattern like this:
<pre class="verbatim">
bundle agent test
{
methods:

 "any" usebundle => get_template("/etc/sudoers","400");
 "any" usebundle => get_template("/etc/hosts","644");

}

</pre>
The the following driving code (based on `copy then edit') can be
placed in a library, after configuring to your environmental
locations:

<pre class="verbatim">bundle agent get_template(final_destination,mode)
{
vars:

 # This needs to ne preconfigured to your site

 "masterfiles"   string => "/home/mark/tmp";
 "this_template" string => lastnode("$(final_destination)","/");

files:

  "$(final_destination).staging"

       comment => "Get template and expand variables for this host",
         perms => mo("400","root"),
     copy_from => remote_cp("$(masterfiles)/templates/$(this_template)","$(policy_server)"),
        action => if_elapsed("60");

  "$(final_destination)"

       comment => "Expand the template",
        create => "true",
     edit_line => expand_template("$(final_destination).staging"),
 edit_defaults => empty,
         perms => mo("$(mode)","root"),
        action => if_elapsed("60");

}

</pre>

<!-- ********************************************************************** -->
<!-- CHAPTER -->
<!-- ********************************************************************** -->
<div class="node">
<a name="Low-level"></a>
<p><hr>
Previous:&nbsp;<a rel="previous" accesskey="p" href="#High-level">High level</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Top">Top</a>

</div>

<h2 class="chapter">3 Low level</h2>

<!--  -->
<!--  -->
<ul class="menu">
<li><a accesskey="1" href="#Aborting-execution">Aborting execution</a>
<li><a accesskey="2" href="#ACL-file-example">ACL file example</a>
<li><a accesskey="3" href="#ACL-generic-example">ACL generic example</a>
<li><a accesskey="4" href="#ACL-secret-example">ACL secret example</a>
<li><a accesskey="5" href="#Active-directory-example">Active directory example</a>
<li><a accesskey="6" href="#Active-list-users-directory-example">Active list users directory example</a>
<li><a accesskey="7" href="#Active-directory-show-users-example">Active directory show users example</a>
<li><a accesskey="8" href="#Add-lines-to-a-file">Add lines to a file</a>
<li><a accesskey="9" href="#Add-users-to-passwd-and-group">Add users to passwd and group</a>
<li><a href="#Add-software-packages-to-the-system">Add software packages to the system</a>
<li><a href="#Add-variable-definitions-to-a-file">Add variable definitions to a file</a>
<li><a href="#Application-baseline">Application baseline</a>
<li><a href="#Array-example">Array example</a>
<li><a href="#Back-references-in-filenames">Back references in filenames</a>
<li><a href="#BSD-flags">BSD flags</a>
<li><a href="#Change-directory-for-command">Change directory for command</a>
<li><a href="#Check-file-or-directory-permissions">Check file or directory permissions</a>
<li><a href="#Class-match-example">Class match example</a>
<li><a href="#Client_002dserver-example">Client-server example</a>
<li><a href="#Commands-example">Commands example</a>
<li><a href="#Commenting-lines-in-a-file">Commenting lines in a file</a>
<li><a href="#Copy-files">Copy files</a>
<li><a href="#Copy-and-flatten-directory">Copy and flatten directory</a>
<li><a href="#Copy-then-edit">Copy then edit</a>
<li><a href="#Creating-files-and-directories">Creating files and directories</a>
<li><a href="#Database-creation">Database creation</a>
<li><a href="#Deleting-lines-from-a-file">Deleting lines from a file</a>
<li><a href="#Deleting-lines-exception">Deleting lines exception</a>
<li><a href="#Editing-files">Editing files</a>
<li><a href="#Editing-tabular-files">Editing tabular files</a>
<li><a href="#Environment-_0028virtual_0029">Environment (virtual)</a>
<li><a href="#Environment-variables">Environment variables</a>
<li><a href="#Execresult-example">Execresult example</a>
<li><a href="#Inserting-lines-in-a-file">Inserting lines in a file</a>
<li><a href="#Get-a-list-of-users">Get a list of users</a>
<li><a href="#Global-classes">Global classes</a>
<li><a href="#Hello-world">Hello world</a>
<li><a href="#LDAP-interactions">LDAP interactions</a>
<li><a href="#Linking-files">Linking files</a>
<li><a href="#Listing-files_002dpattern-in-a-directory">Listing files-pattern in a directory</a>
<li><a href="#Locate-and-transform-files">Locate and transform files</a>
<li><a href="#Logging">Logging</a>
<li><a href="#Measurements">Measurements</a>
<li><a href="#Methods">Methods</a>
<li><a href="#Method-validation">Method validation</a>
<li><a href="#Mount-NFS-filesystem">Mount NFS filesystem</a>
<li><a href="#Ordering-promises">Ordering promises</a>
<li><a href="#Process-management">Process management</a>
<li><a href="#Read-from-a-TCP-socket">Read from a TCP socket</a>
<li><a href="#Resolver-management">Resolver management</a>
<li><a href="#Search-and-replace-text">Search and replace text</a>
<li><a href="#Selecting-a-region-in-a-file">Selecting a region in a file</a>
<li><a href="#Service-management-_0028windows_0029">Service management (windows)</a>
<li><a href="#Set-up-a-PXE-boot-server">Set up a PXE boot server</a>
<li><a href="#Tidying-garbage-files">Tidying garbage files</a>
<li><a href="#Software-distribution">Software distribution</a>
<li><a href="#Trigger-classes">Trigger classes</a>
<li><a href="#Unmount-NFS-filesystem">Unmount NFS filesystem</a>
<li><a href="#Web-server-modules">Web server modules</a>
<li><a href="#Warn-if-matching-line-in-file">Warn if matching line in file</a>
<li><a href="#Windows-registry">Windows registry</a>
<li><a href="#unit_005fregistry_005fcache_002ecf">unit_registry_cache.cf</a>
<li><a href="#unit_005fregistry_002ecf">unit_registry.cf</a>
</ul>

<div class="node">
<a name="Aborting-execution"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#ACL-file-example">ACL file example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Low-level">Low level</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.1 Aborting execution</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "testbundle"  };

version => "1.2.3";
}

###########################################

body agent control

{
abortbundleclasses => { "invalid.Hr16" };
}

###########################################

bundle agent testbundle
{
vars:

 "userlist" slist => { "xyz", "mark", "jeang", "jonhenrik", "thomas", "eben" };

methods:

 "any" usebundle => subtest("$(userlist)");

}

###########################################

bundle agent subtest(user)

{
classes:

  "invalid" not => regcmp("[a-z][a-z][a-z][a-z]","$(user)");

reports:

 !invalid::

  "User name $(user) is valid at 4 letters";

 invalid::

  "User name $(user) is invalid";
}
</pre>

<div class="node">
<a name="ACL-file-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#ACL-generic-example">ACL generic example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Aborting-execution">Aborting execution</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.2 ACL file example</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "acls" };
}

#########################################

bundle agent acls

{
files:

  "/media/flash/acl/test_dir"
 
    depth_search => include_base,
    acl => template;
}

#########################################

body acl template

{
acl_method => "overwrite";
acl_type => "posix";
acl_directory_inherit => "parent";
aces => { "user:*:r(wwx),-r:allow", "group:*:+rw:allow", "mask:x:allow", "all:r"};
}

#########################################

body acl win

{
acl_method => "overwrite";
acl_type => "ntfs";
acl_directory_inherit => "nochange";
aces => { "user:Administrator:rw", "group:Bad:rwx(Dpo):deny" };
}

#########################################

body depth_search include_base

{
include_basedir => "true";
}
</pre>

<div class="node">
<a name="ACL-generic-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#ACL-secret-example">ACL secret example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#ACL-file-example">ACL file example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.3 ACL generic example</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "acls" };
}

#########################################

bundle agent acls

{
files:

  "/media/flash/acl/test_dir"
   
    depth_search => include_base,
    acl => test;
}

#########################################

body acl test

{
acl_type => "generic";
aces => {"user:bob:rwx", "group:staff:rx", "all:r"};
}

#########################################

body depth_search include_base

{
include_basedir => "true";
}
</pre>

<div class="node">
<a name="ACL-secret-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Active-directory-example">Active directory example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#ACL-generic-example">ACL generic example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.4 ACL secret example</h3>

<pre class="verbatim"></pre>

<div class="node">
<a name="Active-directory-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Active-list-users-directory-example">Active list users directory example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#ACL-secret-example">ACL secret example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.5 Active directory example</h3>

<pre class="verbatim"></pre>

<div class="node">
<a name="Active-list-users-directory-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Active-directory-show-users-example">Active directory show users example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Active-directory-example">Active directory example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.6 Active list users directory example</h3>

<pre class="verbatim"></pre>

<div class="node">
<a name="Active-directory-show-users-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Add-lines-to-a-file">Add lines to a file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Active-list-users-directory-example">Active list users directory example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.7 Active directory show users example</h3>

<pre class="verbatim"></pre>

<div class="node">
<a name="Add-lines-to-a-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Add-users-to-passwd-and-group">Add users to passwd and group</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Active-directory-show-users-example">Active directory show users example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.8 Add lines to a file</h3>

<p>There are  numerous approaches to adding lines to a file. 
Often the order of a configuration file is unimportant, we just need to ensure
settings within it. A simple way of adding lines is show below.

<pre class="verbatim">body common control

{
any::

  bundlesequence  => { "insert" };   
}

#######################################################

bundle agent insert

{
vars:

  "lines" string => 
                "
                One potato
                Two potato
                Three potatoe
                Four
                ";
 
files:

  "/tmp/test_insert"

            create => "true",
         edit_line => append_if_no_line("$(insert.lines)");

}

</pre>
Also you could write this using a list variable:
<pre class="verbatim">body common control

{
any::

  bundlesequence  => { "insert" };   
}

#######################################################

bundle agent insert

{
vars:

  "lines" slist => { "One potato", "Two potato",
                "Three potatoe", "Four" };
 
files:

  "/tmp/test_insert"

            create => "true",
         edit_line => append_if_no_line("@(insert.lines)");

}

</pre>

<!--  -->
<div class="node">
<a name="Add-users-to-passwd-and-group"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Add-software-packages-to-the-system">Add software packages to the system</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Add-lines-to-a-file">Add lines to a file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.9 Add users to passwd and group</h3>

<p>Add lines to the password file, and users to group if they are not already there.
<pre class="verbatim">
body common control
{
bundlesequence => { "addpasswd" };
inputs => { "cf_std_library.cf" };
}

bundle agent addpasswd
{
vars:

  # want to set these values by the names of their array keys

  "pwd[mark]" string => "mark:x:1000:100:Mark Burgess:/home/mark:/bin/bash";
  "pwd[fred]" string => "fred:x:1001:100:Right Said:/home/fred:/bin/bash";
  "pwd[jane]" string => "jane:x:1002:100:Jane Doe:/home/jane:/bin/bash";

  "users" slist => getindices("pwd");

files:

  "/etc/passwd"

        create => "true",
     edit_line => append_users_starting("addpasswd.pwd");

  "/etc/group"

       edit_line => append_user_field("users","4","@(addpasswd.users)");

}

</pre>

<!--  -->
<div class="node">
<a name="Add-software-packages-to-the-system"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Add-variable-definitions-to-a-file">Add variable definitions to a file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Add-users-to-passwd-and-group">Add users to passwd and group</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.10 Add software packages to the system</h3>

<pre class="verbatim">#
# Package managment
#

body common control
{
bundlesequence => { "packages" };
}

#############################################

bundle agent packages
{
vars:

 "match_package" slist => { 
                          "apache2", 
                          "apache2-mod_php5",
                          "apache2-prefork",
                          "php5" 
                          };
packages:

 solaris::

  "$(match_package)"

     package_policy => "add",
     package_method => solaris;

 redhat|SuSE::

  "$(match_package)"

     package_policy => "add",
     package_method => yum;

}
</pre>

<p>Note you can also arrange to hide all the differences between package managers
on an OS basis, but since some OSs have multiple managers, this might not
be 100 percent correct.

<!--  -->
<div class="node">
<a name="Add-variable-definitions-to-a-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Application-baseline">Application baseline</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Add-software-packages-to-the-system">Add software packages to the system</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.11 Add variable definitions to a file e.g. <samp><span class="file">/etc/system</span></samp></h3>

<pre class="verbatim">
body common control
{
bundlesequence => { "setvars" };
inputs => { "cf_std_library.cf" };
}


bundle agent setvars
{
vars:

  # want to set these values by the names of their array keys

  "rhs[lhs1]" string => " Mary had a little pig";
  "rhs[lhs2]" string => "Whose Fleece was white as snow";
  "rhs[lhs3]" string => "And everywhere that Mary went";

  # oops, now change pig -> lamb

files:

  "/tmp/system"

        create => "true",
     edit_line => set_variable_values("setvars.rhs");

}

</pre>

<p class="noindent">Results in:

<pre class="verbatim">lhs1= Mary had a little pig
lhs2=Whose Fleece was white as snow
lhs3=And everywhere that Mary went
</pre>

<p class="noindent">An example of this would be to add variables to <samp><span class="file">/etc/sysctl.conf</span></samp> on Linux:

<pre class="verbatim">
body common control
{
bundlesequence => { "setvars" };
inputs => { "cf_std_library.cf" };
}


bundle agent setvars
{
vars:

  # want to set these values by the names of their array keys

  "rhs[net/ipv4/tcp_syncookies]" string => "1";
  "rhs[net/ipv4/icmp_echo_ignore_broadcasts]" string => "1";
  "rhs[net/ipv4/ip_forward]" string => "1";

  # oops, now change pig -> lamb

files:

  "/etc/sysctl"

        create => "true",
     edit_line => set_variable_values("setvars.rhs");

}

</pre>

<ul class="menu">
<li><a accesskey="1" href="#Application-baseline">Application baseline</a>
<li><a accesskey="2" href="#Array-example">Array example</a>
<li><a accesskey="3" href="#Back-references-in-filenames">Back references in filenames</a>
<li><a accesskey="4" href="#BSD-flags">BSD flags</a>
<li><a accesskey="5" href="#Change-directory-for-command">Change directory for command</a>
</ul>

<div class="node">
<a name="Application-baseline"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Array-example">Array example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Add-variable-definitions-to-a-file">Add variable definitions to a file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.12 Application baseline</h3>

<pre class="verbatim"></pre>

<div class="node">
<a name="Array-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Back-references-in-filenames">Back references in filenames</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Application-baseline">Application baseline</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.13 Array example</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "array" };
}


bundle common g
{
vars:

  "array[1]" string => "one"; 
  "array[2]" string => "two"; 
}

bundle agent array
{
vars:

  "localarray[1]" string => "one"; 
  "localarray[2]" string => "two"; 

reports:

 linux::

   "Global $(g.array[1]) and $(localarray[2])";
}

</pre>

<!--  -->
<div class="node">
<a name="Back-references-in-filenames"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#BSD-flags">BSD flags</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Array-example">Array example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.14 Backreferences in filenames</h3>

<pre class="verbatim">
######################################################################
#
# File editing - back reference
#
######################################################################


body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
files:

  # The back reference in a path only applies to the last link
  # of the pathname, so the (tmp) gets ignored

  "/tmp/(cf3)_(.*)"

       edit_line => myedit("second $(match.2)");


  # but ...

#  "/tmp/cf3_test"
#       create    => "true",
#       edit_line => myedit("second $(match.1)");


}

########################################################

bundle edit_line myedit(parameter)
  {
  vars:

   "edit_variable" string => "private edit variable is $(parameter)"; 

  insert_lines:

     "$(edit_variable)";
  
  }

</pre>

<div class="node">
<a name="BSD-flags"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Change-directory-for-command">Change directory for command</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Back-references-in-filenames">Back references in filenames</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.15 BSD flags</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "test" };
}

bundle agent test
{
files:

 freebsd::

   "/tmp/newfile"

       create => "true",
       perms => setbsd;

}


body perms setbsd
{
bsdflags => { "+uappnd","+uchg", "+uunlnk", "-nodump" };
}

</pre>

<!--  -->
<div class="node">
<a name="Change-directory-for-command"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Check-file-or-directory-permissions">Check file or directory permissions</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#BSD-flags">BSD flags</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.16 Change directory for command</h3>

<pre class="verbatim">


body common control

{
bundlesequence  => { "example" };
}

###########################################################

body contain cd(dir) 
{
chdir => "${dir}";
useshell => "true";
}

bundle agent example 
{
commands:

   "/bin/pwd"
       contain => cd("/tmp");
}

</pre>

<div class="node">
<a name="Check-file-or-directory-permissions"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Class-match-example">Class match example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Change-directory-for-command">Change directory for command</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.17 Check file or directory permissions</h3>

<pre class="verbatim">
bundle agent check_perms
{
vars:

  "ns_files" slist => {
                      "/local/iu/logs/admin",
                      "/local/iu/logs/security",
                      "/local/iu/logs/updates",
                      "/local/iu/logs/xfer"
                      };
files:

   NameServers::

    "/local/dns/pz"

            perms => mo("644","dns")
     depth_search => recurse("1"),
      file_select => exclude("secret_file");

    "/local/iu/dns/pz/FixSerial"

            perms => m("755"),
      file_select => plain;

    "$(ns_files)"

            perms => mo("644","dns"),
      file_select => plain;


    "$(ftp)/pub"      
             perms => mog("644","root","other");

    "$(ftp)/pub"      
             perms => m("644"),
      depth_search => recurse("inf");

    "$(ftp)/etc"        perms => mog("111","root","other");
    "$(ftp)/usr/bin/ls" perms => mog("111","root","other");
    "$(ftp)/dev"        perms => mog("555","root","other");
    "$(ftp)/usr"        perms => mog("555","root","other");
}
</pre>

<ul class="menu">
<li><a accesskey="1" href="#Class-match-example">Class match example</a>
<li><a accesskey="2" href="#Client_002dserver-example">Client-server example</a>
<li><a accesskey="3" href="#Commands-example">Commands example</a>
<li><a accesskey="4" href="#Commenting-lines-in-a-file">Commenting lines in a file</a>
</ul>

<div class="node">
<a name="Class-match-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Client_002dserver-example">Client-server example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Check-file-or-directory-permissions">Check file or directory permissions</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.18 Class match example</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "example" };
}

###########################################################

bundle agent example

{     
classes:

  "do_it" and => { classmatch(".*_3"), "linux" }; 

reports:

  do_it::

    "Host matches pattern";

}

</pre>

<div class="node">
<a name="Client-server-example"></a>
<a name="Client_002dserver-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Commands-example">Commands example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Class-match-example">Class match example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.19 Client-server example</h3>

<pre class="verbatim">
########################################################
#
# Simple test copy from server connection to cfServer
#
########################################################

 #
 # run this as follows:
 #
 # cf-serverd -f runtest_1.cf [-v]
 # cf-agent   -f runtest_2.cf
 #
 # Notice that the same file configures all parts of cfengine


########################################################

body common control
{
bundlesequence  => { "testbundle" };
version => "1.2.3";
#fips_mode => "true";
}

########################################################

bundle agent testbundle
{
files: 

  "/home/mark/tmp/testcopy" 
        comment  => "test copy promise",
    copy_from    => mycopy("/home/mark/LapTop/words","127.0.0.1"),
    perms        => system,
    depth_search => recurse("inf"),
    classes      => satisfied("copy_ok");

  "/home/mark/tmp/testcopy/single_file" 

        comment  => "test copy promise",
    copy_from    => mycopy("/home/mark/LapTop/Cfengine3/trunk/README","127.0.0.1"),
    perms        => system;

reports:

  copy_ok::

    "Files were copied..";
}

#########################################################

body perms system

{
mode  => "0644";
}

#########################################################

body depth_search recurse(d)

{
depth => "$(d)";
}

#########################################################

body copy_from mycopy(from,server)

{
source      => "$(from)";
servers     => { "$(server)" };
compare     => "digest";
encrypt     => "true";
verify      => "true";
copy_backup => "true";                  #/false/timestamp
purge       => "false";
type_check  => "true";
force_ipv4  => "true";
trustkey => "true";
}

#########################################################

body classes satisfied(x)
{
promise_repaired => { "$(x)" };
persist_time => "0";
}

#########################################################
# Server config
#########################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "::1" };
allowallconnects      => { "127.0.0.1" , "::1" };
trustkeysfrom         => { "127.0.0.1" , "::1" };
# allowusers
}

#########################################################

bundle server access_rules()

{

access:

  "/home/mark/LapTop"

    admit   => { "127.0.0.1" };
}



</pre>

<div class="node">
<a name="Commands-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Commenting-lines-in-a-file">Commenting lines in a file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Client_002dserver-example">Client-server example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.20 Commands example</h3>

<pre class="verbatim">

body common control
{
bundlesequence  => { "my_commands" };
inputs => { "cfengine_stdlib.cf" };
}


bundle agent my_commands
{
commands:

 Sunday.Hr04.Min05_10.myhost::

  "/usr/bin/update_db";

 any::

  "/etc/mysql/start"

      contain => setuid("mysql");

}


</pre>

<div class="node">
<a name="Commenting-lines-in-a-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Copy-files">Copy files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Commands-example">Commands example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.21 Commenting lines in a file</h3>

<pre class="verbatim">
######################################################################
#
# File editing
#
# Normal ordering:
# - delete
# - replace | colum_edit
# - insert
# 
######################################################################


body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{

files:

  "/home/mark/tmp/cf3_test"

       create    => "true",
       edit_line => myedit("second");
}

########################################################

bundle edit_line myedit(parameter)
  {
  vars:

   "edit_variable" string => "private edit variable is $(parameter)"; 

  
  replace_patterns:

  # replace shell comments with C comments

   "#(.*)"

      replace_with => C_comment,
     select_region => MySection("New section");

  }

########################################
# Bodies
########################################

body replace_with C_comment

{
replace_value => "/* $(match.1) */"; # backreference 0
occurrences => "all";  # first, last all
}

########################################################

body select_region MySection(x)

{
select_start => "\[$(x)\]";
select_end => "\[.*\]";
}
</pre>

<pre class="verbatim">
######################################################################
#
# Comment lines
#
######################################################################

body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/comment_test"

       create    => "true",
       edit_line => comment_lines_matching;
}

########################################################

bundle edit_line comment_lines_matching
  {
  vars:

    "regexes" slist => { "one.*", "two.*", "four.*" };

  replace_patterns:

   "^($(regexes))$"
      replace_with => comment("# ");
  }

########################################
# Bodies
########################################

body replace_with comment(c)

{
replace_value => "$(c) $(match.1)";
occurrences => "all";
}

</pre>

<pre class="verbatim">
######################################################################
#
# Uncomment lines
#
######################################################################

body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

# try this on some test data like

# one
# two
# mark one
#mark two

########################################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/comment_test"

       create    => "true",
       edit_line => uncomment_lines_matching("\s*mark.*","#");
}

########################################################

bundle edit_line uncomment_lines_matching(regex,comment)
{
replace_patterns:

 "#($(regex))$" replace_with => uncomment;
}

########################################################

body replace_with uncomment
{
replace_value => "$(match.1)";
occurrences => "all";
}

</pre>

<div class="node">
<a name="Copy-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Copy-and-flatten-directory">Copy and flatten directory</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Commenting-lines-in-a-file">Commenting lines in a file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.22 Copy files</h3>

<pre class="verbatim">files:

  "/var/cfengine/inputs" 

    handle => "update_policy",
    perms => m("600"),
    copy_from => u_scp("$(master_location)",@(policy_server)),
    depth_search => recurse("inf"),
    file_select => input_files,
    action => immediate;

  "/var/cfengine/bin" 

    perms => m("700"),
    copy_from => u_scp("/usr/local/sbin","localhost"),
    depth_search => recurse("inf"),
    file_select => cf3_files,
    action => immediate,
    classes => on_change("reload");

</pre>

<ul class="menu">
<li><a accesskey="1" href="#Copy-and-flatten-directory">Copy and flatten directory</a>
</ul>

<div class="node">
<a name="Copy-and-flatten-directory"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Copy-then-edit">Copy then edit</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Copy-files">Copy files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.23 Copy and flatten directory</h3>

<pre class="verbatim">
########################################################
#
# Simple test copy from server connection to cfServer
#
########################################################

 #
 # run this as follows:
 #
 # cf-serverd -f runtest_1.cf [-d2]
 # cf-agent   -f runtest_2.cf
 #
 # Notice that the same file configures all parts of cfengine

########################################################

body common control

{
bundlesequence  => { "testbundle" };
version => "1.2.3";
}

########################################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/testflatcopy" 

        comment  => "test copy promise",
    copy_from    => mycopy("/home/mark/LapTop/words","127.0.0.1"),
    perms        => system,
    depth_search => recurse("inf"),
    classes      => satisfied("copy_ok");


  "/home/mark/tmp/testcopy/single_file" 

        comment  => "test copy promise",
    copy_from    => mycopy("/home/mark/LapTop/Cfengine3/trunk/README","127.0.0.1"),
    perms        => system;

reports:

  copy_ok::

    "Files were copied..";
}

#########################################################

body perms system

{
mode  => "0644";
}

#########################################################

body depth_search recurse(d)

{
depth => "$(d)";
}

#########################################################

body copy_from mycopy(from,server)

{
source      => "$(from)";
servers     => { "$(server)" };
compare     => "digest";
verify      => "true";
copy_backup => "true";                  #/false/timestamp
purge       => "false";
type_check  => "true";
force_ipv4  => "true";
trustkey => "true";
collapse_destination_dir => "true";
}

#########################################################

body classes satisfied(x)
{
promise_repaired => { "$(x)" };
persist_time => "0";
}

#########################################################
# Server config
#########################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "::1" };
allowallconnects      => { "127.0.0.1" , "::1" };
trustkeysfrom         => { "127.0.0.1" , "::1" };
}

#########################################################

bundle server access_rules()

{
access:

  "/home/mark/LapTop"

    admit   => { "127.0.0.1" };
}

</pre>

<div class="node">
<a name="Copy-then-edit"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Creating-files-and-directories">Creating files and directories</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Copy-and-flatten-directory">Copy and flatten directory</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.24 Copy then edit a file convergently</h3>

<p>To convergently chain a copy followed by edit, you need a staging file. 
First you copy to the staging file. Then you edit the final file and
insert the staging file into it as part of the editing. This is convergent
with respect to both stages of the process.

<pre class="verbatim">bundle agent master
{
files:

  "$(final_destination)"

         create => "true",
     edit_line => fix_file("$(staging_file)"),
 edit_defaults => empty,
         perms => mo("644","root"),
        action => ifelapsed("60");
}

#

bundle edit_line fix_file(f)
{
insert_lines:

  "$(f)"
 
     # insert this into an empty file to reconstruct
 
     insert_type => "file";

replace_patterns:

    "searchstring"

          replace_with => With("replacestring");
}


</pre>

<ul class="menu">
<li><a accesskey="1" href="#Creating-files-and-directories">Creating files and directories</a>
<li><a accesskey="2" href="#Database-creation">Database creation</a>
<li><a accesskey="3" href="#Deleting-lines-from-a-file">Deleting lines from a file</a>
<li><a accesskey="4" href="#Deleting-lines-exception">Deleting lines exception</a>
</ul>

<div class="node">
<a name="Creating-files-and-directories"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Database-creation">Database creation</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Copy-then-edit">Copy then edit</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.25 Creating files and directories</h3>

<pre class="verbatim">
########################################################
#
# Simple test create files
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/test_plain" 

       perms => system,
       create => "true";

  "/home/mark/tmp/test_dir/." 

       perms => system,
       create => "true";

}

#########################################################

body perms system

{
mode  => "0640";
}

#########################################################

body depth_search recurse(d)

{
depth => "$(d)";
}

</pre>

<!--  -->
<div class="node">
<a name="Database-creation"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Deleting-lines-from-a-file">Deleting lines from a file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Creating-files-and-directories">Creating files and directories</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.26 Database creation</h3>

<pre class="verbatim">
body common control
{
bundlesequence => { "dummy" };
}

body knowledge control

{
#sql_database => "postgres";
sql_owner => "postgres";
sql_passwd => ""; # No passwd
sql_type => "postgres";
}

bundle knowledge dummy
{
topics:
}
</pre>

<pre class="verbatim">

body common control
{
bundlesequence => { "databases" };
}

bundle agent databases

{
#commands:

#  "/usr/bin/createdb cf_topic_maps",

#        contain => as_user("mysql");

databases:

  "knowledge_bank/topics"

    database_operation => "create",
    database_type => "sql",
    database_columns => { 
                        "topic_name,varchar,256",
                        "topic_comment,varchar,1024",
                        "topic_id,varchar,256",
                        "topic_type,varchar,256",
                        "topic_extra,varchar,26" 
                        },

    database_server => myserver;



}

################################################

body database_server myserver
{
none::
 db_server_owner => "postgres";
 db_server_password => "";
 db_server_host => "localhost";
 db_server_type => "postgres";
 db_server_connection_db => "postgres";
any::
 db_server_owner => "root";
 db_server_password => "";
 db_server_host => "localhost";
 db_server_type => "mysql";
 db_server_connection_db => "mysql";
}

body contain as_user(x)
{
exec_owner => "$(x)";
}
</pre>

<div class="node">
<a name="Deleting-lines-from-a-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Deleting-lines-exception">Deleting lines exception</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Database-creation">Database creation</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.27 Deleting lines from a file</h3>

<pre class="verbatim">


body common control
{
bundlesequence => { "test" };
}



bundle agent test
{
files:

  "/tmp/resolv.conf"  # test on "/tmp/resolv.conf" #

     create        => "true",
     edit_line     => resolver,
     edit_defaults => def;

}


#######################################################
# For the library
#######################################################

bundle edit_line resolver

{
vars:

 "search" slist => { "search iu.hio.no cfengine.com", "nameserver 128.39.89.10" };

delete_lines:

  "search.*";

insert_lines:

  "$(search)" location => end;
}

#######################################################

body edit_defaults def
{
empty_file_before_editing => "false";
edit_backup => "false";
max_file_size => "100000";
}

########################################################

body location start

{
# If not line to match, applies to whole text body
before_after => "before";
}

########################################################

body location end

{
# If not line to match, applies to whole text body
before_after => "after";
}
</pre>

<div class="node">
<a name="Deleting-lines-exception"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Editing-files">Editing files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Deleting-lines-from-a-file">Deleting lines from a file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.28 Deleting lines exception</h3>

<pre class="verbatim">
########################################################
#
# Simple test editfile
#
########################################################

#
# This assumes a file format like:
#
# [section 1]
#
# lines....
#
# [section 2]
#
# lines... etc


body common control

{
bundlesequence  => { "testbundle" };
}

########################################################

bundle agent testbundle

{
files:

  "/tmp/passwd_excerpt"

       create    => "true",
       edit_line => MarkNRoot;
}

########################################################

bundle edit_line MarkNRoot
  {
  delete_lines:

       "mark.*|root.*" not_matching => "true";

  }

</pre>

<!--  -->
<div class="node">
<a name="Editing-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Editing-tabular-files">Editing tabular files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Deleting-lines-exception">Deleting lines exception</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.29 Editing files</h3>

<p>This is a huge topic. See also See <a href="#Add-lines-to-a-file">Add lines to a file</a>, See <a href="#Editing-tabular-files">Editing tabular files</a>, etc. 
Editing a file can be complex or simple, depending on needs.

   <p>Here is an example of how to comment out lines matching a number of patterns:
<pre class="verbatim">######################################################################
#
# Comment lines
#
######################################################################

body common control

{
version         =>   "1.2.3";
bundlesequence  => { "testbundle"  };
inputs          => { "cf_std_library.cf" };
}

########################################################

bundle agent testbundle

{
vars:

  "patterns" slist => { "finger.*", "echo.*", "exec.*", "rstat.*", 
                                               "uucp.*", "talk.*" };

files:

  "/etc/inetd.conf"

      edit_line => comment_lines_matching("@(testbundle.patterns)","#");
}

</pre>

<!--  -->
<div class="node">
<a name="Editing-tabular-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Environment-_0028virtual_0029">Environment (virtual)</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Editing-files">Editing files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.30 Editing tabular files</h3>

<pre class="verbatim">
######################################################################
#
# File editing
#
# Normal ordering:
# - delete
# - replace | colum_edit
# - insert
# 
######################################################################


body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
vars:

 "userset" slist => { "one-x", "two-x", "three-x" };

files:

  # Make a copy of the password file

  "/home/mark/tmp/passwd"

       create    => "true",
       edit_line => SetUserParam("mark","6","/set/this/shell");

  "/home/mark/tmp/group"

       create    => "true",
       edit_line => AppendUserParam("root","4","@(userset)");

commands:

  "/bin/echo" args => "$(userset)";

}

########################################################

bundle edit_line SetUserParam(user,field,val)
  {
  field_edits:

   "$(user):.*"

      # Set field of the file to parameter

      edit_field => col(":","$(field)","$(val)","set");
  }

########################################################

bundle edit_line AppendUserParam(user,field,allusers)
  {
  vars:

    "val" slist => { @(allusers) };

  field_edits:

   "$(user):.*"

      # Set field of the file to parameter

      edit_field => col(":","$(field)","$(val)","alphanum");

  }

########################################
# Bodies
########################################

body edit_field col(split,col,newval,method)

{
field_separator => "$(split)";
select_field    => "$(col)";
value_separator  => ",";
field_value     => "$(newval)";
field_operation => "$(method)";
extend_fields => "true";
}


</pre>

<ul class="menu">
<li><a accesskey="1" href="#Environment-_0028virtual_0029">Environment (virtual)</a>
<li><a accesskey="2" href="#Environment-variables">Environment variables</a>
<li><a accesskey="3" href="#Execresult-example">Execresult example</a>
<li><a accesskey="4" href="#Inserting-lines-in-a-file">Inserting lines in a file</a>
<li><a accesskey="5" href="#Get-a-list-of-users">Get a list of users</a>
<li><a accesskey="6" href="#Global-classes">Global classes</a>
<li><a accesskey="7" href="#Hello-world">Hello world</a>
<li><a accesskey="8" href="#LDAP-interactions">LDAP interactions</a>
<li><a accesskey="9" href="#Linking-files">Linking files</a>
<li><a href="#Listing-files_002dpattern-in-a-directory">Listing files-pattern in a directory</a>
<li><a href="#Locate-and-transform-files">Locate and transform files</a>
<li><a href="#Logging">Logging</a>
<li><a href="#Measurements">Measurements</a>
<li><a href="#Methods">Methods</a>
<li><a href="#Method-validation">Method validation</a>
<li><a href="#Mount-NFS-filesystem">Mount NFS filesystem</a>
</ul>

<div class="node">
<a name="Environment-(virtual)"></a>
<a name="Environment-_0028virtual_0029"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Environment-variables">Environment variables</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Editing-tabular-files">Editing tabular files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.31 Environments (virtual)</h3>

<pre class="verbatim">
#######################################################
#
# Virtual environments
#
#######################################################

body common control

{
bundlesequence  => { "my_vm_cloud" };   
}

#######################################################

bundle agent my_vm_cloud

{
vars:

  "vms[atlas]" slist => { "guest1", "guest2", "guest3" };

environments:

 scope||any::  # These should probably be in class "any" to ensure uniqueness

   "$(vms[$(sys.host)])"

       environment_resources => virt_xml("$(xmlfile[$(this.promiser)])"),
       environment_interface => vnet("eth0,192.168.1.100/24"),
       environment_type      => "test",
       environment_host      => "atlas";

      # default environment_state => "create" on host, and "suspended elsewhere"
}

#######################################################

body environment_resources virt_xml(specfile)
{
env_spec_file => "$(specfile)";
}

#######################################################

body environment_interface vnet(primary)
{
env_name      => "$(this.promiser)";
env_addresses => { "$(primary)" };

host1::

  env_network => "default_vnet1";

host2::

  env_network => "default_vnet2";

}
</pre>

<div class="node">
<a name="Environment-variables"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Execresult-example">Execresult example</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Environment-_0028virtual_0029">Environment (virtual)</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.32 Environment variables</h3>

<pre class="verbatim"></pre>

<pre class="verbatim">
#######################################################
#
# Virtual environments
#
#######################################################

body common control

{
bundlesequence  => { "my_vm_cloud" };   
}

#######################################################

bundle agent my_vm_cloud

{
environments:

   "centos5"

       environment_resources => virt_xml,
       environment_type      => "xen",
       environment_host      => "ursa-minor";

      # default environment_state => "create" on host, and "suspended elsewhere"
}

#######################################################

body environment_resources virt_xml
{
env_spec_file => "/srv/xen/centos5-libvirt-create.xml";
}

</pre>

<div class="node">
<a name="Execresult-example"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Inserting-lines-in-a-file">Inserting lines in a file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Environment-variables">Environment variables</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.33 Execresult example</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "example" };
}

###########################################################

bundle agent example

{     
vars:

  "my_result" string => execresult("/bin/ls /tmp","noshell");

reports:

  linux::

    "Variable is $(my_result)";

}

</pre>

<!--  -->
<div class="node">
<a name="Inserting-lines-in-a-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Get-a-list-of-users">Get a list of users</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Execresult-example">Execresult example</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.34 Inserting lines in a file</h3>

<pre class="verbatim">

#######################################################
#
# Insert a number of lines with vague whitespace
#
#######################################################

body common control

{
any::

  bundlesequence  => { "insert" };   
}


#######################################################

bundle agent insert

{
vars:

  "v" string => "  One potato";
 
files:

  "/tmp/test_insert"

            create => "true",
         edit_line => Insert("$(insert.v)");

}

#######################################################
# For the library
#######################################################

bundle edit_line Insert(name)

{
insert_lines:

  "  $(name)"

      whitespace_policy => { "ignore_leading", "ignore_embedded" };

}

#######################################################

body edit_defaults empty

{
empty_file_before_editing => "true";
}
</pre>

<pre class="verbatim">

#######################################################
#
# Insert a number of lines
#
#######################################################

body common control

{
any::

  bundlesequence  => { "insert" };   
}


#######################################################

bundle agent insert

{
vars:

  "v" string => "
                One potato
                Two potato
                Three potatoe
                Four
                ";
 
files:

  "/tmp/test_insert"

            create => "true",
         edit_line => Insert("$(insert.v)"),
     edit_defaults => empty;

}

#######################################################
# For the library
#######################################################

bundle edit_line Insert(name)

{
insert_lines:

  "Begin$(const.n)$(name)$(const.n)End";

}

#######################################################

body edit_defaults empty

{
empty_file_before_editing => "false";
}
</pre>

<pre class="verbatim">

#######################################################
#
# Insert a number of lines
#
#######################################################

body common control

{
any::

  bundlesequence  => { "insert" };   
}


#######################################################

bundle agent insert

{
vars:

  "v" slist => {
                "One potato",
                "Two potato",
                "Three potatoe",
                "Four"    
               };
 
files:

  "/tmp/test_insert"

            create => "true",
         edit_line => Insert("@(insert.v)");
   #  edit_defaults => empty;

}

#######################################################
# For the library
#######################################################

bundle edit_line Insert(name)

{
insert_lines:

  "$(name)";

}

#######################################################

body edit_defaults empty

{
empty_file_before_editing => "true";
}
</pre>

<!--  -->
<div class="node">
<a name="Get-a-list-of-users"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Global-classes">Global classes</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Inserting-lines-in-a-file">Inserting lines in a file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.35 Get a list of users</h3>

<pre class="verbatim">
#######################################################
#
# GetUsers
#
#######################################################

body common control

{
any::

  bundlesequence  => {
                     test
                     };   
}

#######################################################

bundle agent test

{
vars:

  "allusers" slist => getusers("zenoss,mysql,at","12,0");

reports:

 linux::

  "Found user $(allusers)";

}
</pre>

<div class="node">
<a name="Global-classes"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Hello-world">Hello world</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Get-a-list-of-users">Get a list of users</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.36 Global classes</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "g","tryclasses_1", "tryclasses_2" };

}

#################################

bundle common g
{
classes:

  "one" expression => "any";

  "client_network" expression => iprange("128.39.89.0/24");
}

#################################

bundle agent tryclasses_1
{
classes:

  "two" expression => "any";
}

#################################

bundle agent tryclasses_2
{
classes:

  "three" expression => "any";

reports:

  one.three.!two::

    "Success";
}


#################################
</pre>

<!--  -->
<div class="node">
<a name="Hello-world"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#LDAP-interactions">LDAP interactions</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Global-classes">Global classes</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.37 Hello world</h3>

<pre class="verbatim">
# Hard promises

body common control
{
bundlesequence => { "hello" };
}

# soft promises

bundle agent hello
{
reports:

 linux::

   "Hello world!";
}
</pre>

<!--  -->
<div class="node">
<a name="LDAP-interactions"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Linking-files">Linking files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Hello-world">Hello world</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.38 LDAP interactions</h3>

<pre class="verbatim">

#

body common control
{
bundlesequence => { "ldap" , "followup"};
}

###################################################################################################
#  NOTE!! relying on LDAP or other network data without validation is EXTREMELY dangerous. 
#         You could destroy a system by assuming that the service will respond with a 
#         sensible result. Cfengine does not recommend reliance on network services in configuration.
###################################################################################################

bundle agent ldap
{
vars:

   # Get the first matching value for "uid"

  "value" string => ldapvalue("ldap://eternity.iu.hio.no","dc=cfengine,dc=com","(sn=User)","uid","subtree","none");

   # Geta all matching values for "uid" - should be a single record match

  "list" slist =>  ldaplist("ldap://eternity.iu.hio.no","dc=cfengine,dc=com","(sn=User)","uid","subtree","none");

classes:

   "gotdata" expression => ldaparray("myarray","ldap://eternity.iu.hio.no","dc=cfengine,dc=com","(uid=mark)","subtree","none");

   "found" expression => regldap("ldap://eternity.iu.hio.no","dc=cfengine,dc=com","(sn=User)","uid","subtree","jon.*","none");

reports:

 linux::

   "LDAP VALUE $(value) found";
   "LDAP LIST VALUE $(list)";

 gotdata::

   "Found specific entry data  ...$(ldap.myarray[uid]),$(ldap.myarray[gecos]), etc";

  found::

    "Matched regex";

}

bundle agent followup

{
reports:

 linux::

   "Different bundle  ...$(ldap.myarray[uid]),$(ldap.myarray[gecos]), etc";

}
</pre>

<div class="node">
<a name="Linking-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Listing-files_002dpattern-in-a-directory">Listing files-pattern in a directory</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#LDAP-interactions">LDAP interactions</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.39 Linking files</h3>

<pre class="verbatim">
######################################################################
#
# File editing
#
# Normal ordering:
# - delete
# - replace | colum_edit
# - insert
# 
######################################################################


body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
files:

  # Make a copy of the password file

  "/home/mark/tmp/passwd"

       link_from     => linkdetails("/etc/passwd"),
       move_obstructions => "true";


  "/home/mark/tmp/linktest"

       link_from     => linkchildren("/usr/local/sbin");


#child links
}

#########################################################

body link_from linkdetails(tofile)

{
source        => "$(tofile)";
link_type     => "symlink";
when_no_source  => "force";      # kill
}

#########################################################

body link_from linkchildren(tofile)

{
source        => "$(tofile)";
link_type     => "symlink";
when_no_source  => "force";      # kill
link_children => "true";
when_linking_children => "if_no_such_file"; # "override_file";
}


</pre>

<p>Removing deadlinks from a directory:
<pre class="verbatim">
#######################################################
#
# Test dead link removal
#
#######################################################

body common control
   {
   any::

      bundlesequence  => { 
                         "testbundle"
                         };
   }


############################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/test_to" -> "someone"

      depth_search => recurse("inf"),
      perms => modestuff,
      action => tell_me;

}

############################################

body depth_search recurse(d)

{
rmdeadlinks => "true";
depth => "$(d)";
}

############################################

body perms modestuff

{
mode => "o-w";
}

############################################

body action tell_me

{
report_level => "inform";
}
</pre>

<div class="node">
<a name="Listing-files-pattern-in-a-directory"></a>
<a name="Listing-files_002dpattern-in-a-directory"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Locate-and-transform-files">Locate and transform files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Linking-files">Linking files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.40 Listing files-pattern in a directory</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "example" };
}

###########################################################

bundle agent example

{     
vars:

  "ls" slist => lsdir("/etc","p.*","true");

reports:

  !sdfkjh::

    "ls: $(ls)";

}


</pre>

<div class="node">
<a name="Locate-and-transform-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Logging">Logging</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Listing-files_002dpattern-in-a-directory">Listing files-pattern in a directory</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.41 Locate and transform files</h3>

<pre class="verbatim">
#######################################################
#
# Compressing files
#
#######################################################

body common control
   {
   any::

      bundlesequence  => { 
                         "testbundle"
                         };

   version => "1.2.3";
   }

############################################

bundle agent testbundle

{
files:

  "/home/mark/tmp/testcopy" 

    file_select => pdf_files,
    transformer => "/usr/bin/gzip $(this.promiser)",
    depth_search => recurse("inf");

}

############################################

body file_select pdf_files

{
leaf_name => { ".*.pdf" , ".*.fdf" };
file_result => "leaf_name";
}

############################################

body depth_search recurse(d)

{
depth => "$(d)";
}

</pre>

<div class="node">
<a name="Logging"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Measurements">Measurements</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Locate-and-transform-files">Locate and transform files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.42 Logging</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "test" };
}

bundle agent test
{
vars:

  "software" slist => { "/root/xyz", "/tmp/xyz" };

files:

  "$(software)"

    create => "true",
     action => logme("$(software)");

}

#

body action logme(x)
{
log_kept => "/tmp/private_keptlog.log";
log_failed => "/tmp/private_faillog.log";
log_repaired => "/tmp/private_replog.log";
log_string => "$(sys.date) $(x) promise status";
}
</pre>

<pre class="verbatim"></pre>

<pre class="verbatim">

body common control
{
bundlesequence => { "one" };
}



bundle agent one
{
files:

  "/tmp/xyz"

       create => "true",
       action => log;

}

body action log
{
log_level => "inform";
}


</pre>

<!--  -->
<div class="node">
<a name="Measurements"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Methods">Methods</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Logging">Logging</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.43 Measurements</h3>

<pre class="verbatim"></pre>

<pre class="verbatim">
#cop measurements,example

#######################################################
#
# Test file:
#
# First line
# Blonk blonk bnklkygsuilnm
#
#######################################################

body common control
{
bundlesequence => { "report" };
}

#######################################################

body monitor control
{
forgetrate => "0.7";
histograms => "true";
}

#######################################################

bundle agent report
{
reports:

 cfengine_3::

   "
   Free memory read at $(mon.av_free_memory_watch)
   cf_monitord read $(mon.value_monitor_self_watch)   
   ";
}

#######################################################

bundle monitor watch
{
measurements:

  # Test 1 - extract string matching

  "/home/mark/tmp/testmeasure"

      handle => "blonk_watch",
      stream_type => "file",
      data_type => "string",
      history_type => "weekly",
      units => "blonks",
      match_value => find_blonks,
      action => sample_min("10");

  # Test 2 - follow a special process over time
  # using cfengine's process cache to avoid resampling

   "/var/cfengine/state/cf_rootprocs"

      handle => "monitor_self_watch",
      stream_type => "file",
      data_type => "int",
      history_type => "static",
      units => "kB",
      match_value => proc_value(".*cf-monitord.*",
                                "root\s+[0-9.]+\s+[0-9.]+\s+[0-9.]+\s+[0-9.]+\s+([0-9]+).*");

  # Test 3, discover disk device information

  "/bin/df"

      handle => "free_disk_watch",
      stream_type => "pipe",
      data_type => "slist",
      history_type => "static",
      units => "device",
      match_value => file_system;
      # Update this as often as possible

  # Test 4

   "/tmp/file"

         handle => "line_counter",
    stream_type => "file",
      data_type => "counter",
    match_value => scanlines("MYLINE.*"),
   history_type => "log";

}

##########################################################

body match_value scanlines(x)
{
select_line_matching => "^$(x)$";
}

##########################################################

body action sample_min(x)
{
ifelapsed => "$(x)";
expireafter => "$(x)";
}

##########################################################

body match_value find_blonks
{
select_line_number => "2";
extraction_regex => "Blonk blonk ([blonk]+).*";
}

##########################################################

body match_value free_memory # not willy!
{
select_line_matching => "MemFree:.*";
extraction_regex => "MemFree:\s+([0-9]+).*";
}

##########################################################

body match_value proc_value(x,y)
{
select_line_matching => "$(x)";
extraction_regex => "$(y)";
}

##########################################################

body match_value file_system
{
select_line_matching => "/.*";
extraction_regex => "(.*)";
}

</pre>

<div class="node">
<a name="Methods"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Method-validation">Method validation</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Measurements">Measurements</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.44 Methods</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "testbundle"  };

version => "1.2.3";
}

###########################################

bundle agent testbundle
{
vars:

 "userlist" slist => { "mark", "jeang", "jonhenrik", "thomas", "eben" };

methods:

 "any" usebundle => subtest("$(userlist)");

}

###########################################

bundle agent subtest(user)

{
commands:

 "/bin/echo Fix $(user)";

reports:

 linux::

  "Finished doing stuff for $(user)";
}

</pre>

<div class="node">
<a name="Method-validation"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Mount-NFS-filesystem">Mount NFS filesystem</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Methods">Methods</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.45 Method validation</h3>

<pre class="verbatim">

body common control

{
bundlesequence  => { "testbundle"  };

version => "1.2.3";
}

###########################################

body agent control

{
abortbundleclasses => { "invalid" };
}

###########################################

bundle agent testbundle
{
vars:

 "userlist" slist => { "xyz", "mark", "jeang", "jonhenrik", "thomas", "eben" };

methods:

 "any" usebundle => subtest("$(userlist)");

}

###########################################

bundle agent subtest(user)

{
classes:

  "invalid" not => regcmp("[a-z][a-z][a-z][a-z]","$(user)");

reports:

 !invalid::

  "User name $(user) is valid at 4 letters";

 invalid::

  "User name $(user) is invalid";
}
</pre>

<div class="node">
<a name="Mount-NFS-filesystem"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Ordering-promises">Ordering promises</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Method-validation">Method validation</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.46 Mount NFS filesystem</h3>

<pre class="verbatim">

#
# cfengine 3
#
# cf-agent -f ./cftest.cf -K
#

body common control

{
bundlesequence => { "mounts" };
}

#

bundle agent mounts

{
storage:

  "/mnt" mount  => nfs("slogans.iu.hio.no","/home");

}

######################################################################

body mount nfs(server,source)

{
mount_type => "nfs";
mount_source => "$(source)";
mount_server => "$(server)";
#mount_options => { "rw" };
edit_fstab => "true";
unmount => "true";
}
</pre>

<div class="node">
<a name="Ordering-promises"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Process-management">Process management</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Mount-NFS-filesystem">Mount NFS filesystem</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.47 Ordering promises</h3>

<p>This counts to five by default. If we change &lsquo;<samp><span class="samp">/bin/echo one</span></samp>&rsquo;
to &lsquo;<samp><span class="samp">/bin/echox one</span></samp>&rsquo;, then the command will fail, causing us
to skip five and go to six instead.

   <p>This shows how dependencies can be chained in spite of the
order of promises in the bundle.

   <p>Normally the order of promises in a bundle is followed, within each
promise type, and the types are ordered according to <i>normal
ordering</i>.

<pre class="verbatim">
##################################################################
#
# cfengine 3 - ordering promises into dependent chains
#
##
#
# cf-agent -f ./cftest.cf -K
#
##################################################################

body common control

{
bundlesequence => { "order" };
}

##################################################################

bundle agent order

{
vars:

 "list" slist => { "three", "four" };

commands:

 ok_later::

   "/bin/echo five";

 otherthing::

   "/bin/echo six";

 any::


  "/bin/echo one"     classes => d("ok_later","otherthing");
  "/bin/echo two";
  "/bin/echo $(list)";

 preserved_class::

  "/bin/echo seven";

}

############################################

body classes d(if,else)

{
promise_repaired => { "$(if)" };
repair_failed => { "$(else)" };
persist_time => "0";
}
</pre>

<!--  -->
<ul class="menu">
<li><a accesskey="1" href="#Process-management">Process management</a>
<li><a accesskey="2" href="#Read-from-a-TCP-socket">Read from a TCP socket</a>
<li><a accesskey="3" href="#Resolver-management">Resolver management</a>
<li><a accesskey="4" href="#Search-and-replace-text">Search and replace text</a>
<li><a accesskey="5" href="#Selecting-a-region-in-a-file">Selecting a region in a file</a>
<li><a accesskey="6" href="#Service-management-_0028windows_0029">Service management (windows)</a>
</ul>

<div class="node">
<a name="Process-management"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Read-from-a-TCP-socket">Read from a TCP socket</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Ordering-promises">Ordering promises</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.48 Process management</h3>

<pre class="verbatim">
body common control
{
bundlesequence => { "test" };
}



bundle agent test
{
processes:

 "sleep"

   signals => { "term", "kill" };

}
</pre>

<pre class="verbatim">
########################################################
#
# Simple test processes 
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
processes:

 "sleep"

    process_count   => up("sleep");

reports:

 sleep_out_of_control::

   "Out of control";
}

########################################################

body process_count up(s)

{
match_range => "5,10"; # or irange("1","10");
out_of_range_define => { "$(s)_out_of_control" };
}

</pre>

<pre class="verbatim">
########################################################
#
# Simple test processes 
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
processes:

 ".*"

    process_select  => proc_finder("a.*"),
    process_count   => up("cfservd");
}

########################################################

body process_count up(s)

{
match_range => "1,10"; # or irange("1","10");
out_of_range_define => { "$(s)_out_of_control" };
}

########################################################

body process_select proc_finder(p)

{
stime_range => irange(ago("0","0","0","2","0","0"),now);
process_result => "stime";
}


</pre>

<pre class="verbatim">
########################################################
#
# Simple test processes 
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
processes:

 ".*"

    process_select  => proc_finder("a.*"),
    process_count   => up("cfservd");
}

########################################################

body process_count up(s)

{
match_range => "1,10"; # or irange("1","10");
out_of_range_define => { "$(s)_out_of_control" };
}

########################################################

body process_select proc_finder(p)

{
process_owner  => { "avahi", "bin" };
command        => "$(p)";
pid            => "100,199";
vsize          => "0,1000";
process_result => "command.(process_owner|vsize)";
}


</pre>

<pre class="verbatim"></pre>

<pre class="verbatim"></pre>

<pre class="verbatim">
########################################################
#
# Simple test process restart
#
########################################################

body common control

{
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{
processes:

 "cfservd"

        process_count   => up("cfservd");

 cfservd_out_of_control::

   "cfservd"

        signals         => { "stop" , "term" },
        restart_class   => "start_cfserv";


commands:

  start_cfserv::

    "/usr/local/sbin/cfservd";

}

########################################################

body process_count up(s)

{
match_range => "1,10"; # or irange("1","10");
out_of_range_define => { "$(s)_out_of_control" };
}
</pre>

<!--  -->
<div class="node">
<a name="Read-from-a-TCP-socket"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Resolver-management">Resolver management</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Process-management">Process management</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.49 Read from a TCP socket</h3>

<pre class="verbatim">


body common control

{
bundlesequence  => { "example" };
}

###########################################################

bundle agent example

{     
vars:

  "my80" string => readtcp("research.iu.hio.no","80","GET /index.php HTTP/1.1$(const.r)$(const.n)Host: research.iu.hio.no$(const.r)$(const.n)$(const.r)$(const.n)",20);

classes:

  "server_ok" expression => regcmp(".*200 OK.*\n.*","$(my80)");

reports:

  server_ok::

    "Server is alive";

  !server_ok::

    "Server is not responding - got $(my80)";
}


</pre>

<div class="node">
<a name="Resolver-management"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Search-and-replace-text">Search and replace text</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Read-from-a-TCP-socket">Read from a TCP socket</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.50 Resolver management</h3>

<pre class="verbatim">
#######################################################
#
# Resolve conf
#
#######################################################

bundle common g # globals
{
vars:

 "searchlist"  slist => { 
                        "search iu.hio.no", 
                        "search cfengine.com" 
                        };

 "nameservers" slist => { 
                        "128.39.89.10", 
                        "128.39.74.16",
                        "192.168.1.103"
                        };
classes:

  "am_name_server" expression => reglist("@(nameservers)","$(sys.ipv4[eth1])");
}

#######################################################

body common control

{
any::

  bundlesequence  => {
                     "g",
                     resolver(@(g.searchlist),@(g.nameservers))
                     };   

  domain => "iu.hio.no";
}

#######################################################

bundle agent resolver(s,n)

{
files:

  # When passing parameters down, we have to refer to
  # a source context

  "$(sys.resolv)"  # test on "/tmp/resolv.conf" #

      create        => "true",
      edit_line     => doresolv("@(this.s)","@(this.n)"),
      edit_defaults => reconstruct;
 # or edit_defaults => modify
}

#######################################################
# For the library
#######################################################

bundle edit_line doresolv(s,n)

{
vars:

 "line" slist => { @(s), @(n) };

insert_lines:

  "$(line)";

}

#######################################################

body edit_defaults reconstruct
{
empty_file_before_editing => "true";
edit_backup => "false";
max_file_size => "100000";
}

#######################################################

body edit_defaults modify
{
empty_file_before_editing => "false";
edit_backup => "false";
max_file_size => "100000";
}


</pre>

<!--  -->
<div class="node">
<a name="Search-and-replace-text"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Selecting-a-region-in-a-file">Selecting a region in a file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Resolver-management">Resolver management</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.51 Search and replace text</h3>

<pre class="verbatim">
######################################################################
#
# File editing
#
# Normal ordering:
# - delete
# - replace | colum_edit
# - insert
# 
######################################################################


body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{

files:

  "/tmp/replacestring"

       create    => "true",
       edit_line => myedit("second");
}

########################################################

bundle edit_line myedit(parameter)
  {
  vars:

   "edit_variable" string => "private edit variable is $(parameter)"; 

  
  replace_patterns:

  # replace shell comments with C comments

   "puppet"

      replace_with => With("cfengine 3");

  }

########################################
# Bodies
########################################

body replace_with With(x)

{
replace_value => "$(x)";
occurrences => "first";
}

########################################

body select_region MySection(x)

{
select_start => "\[$(x)\]";
select_end => "\[.*\]";
}

</pre>

<div class="node">
<a name="Selecting-a-region-in-a-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Service-management-_0028windows_0029">Service management (windows)</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Search-and-replace-text">Search and replace text</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.52 Selecting a region in a file</h3>

<pre class="verbatim">

body common control

{
version => "1.2.3";
bundlesequence  => { "testbundle"  };
}

########################################################

bundle agent testbundle

{

files:

  "/tmp/testfile"

       create    => "true",
       edit_line => myedit("second");
}

########################################################

bundle edit_line myedit(parameter)
  {
  vars:

   "edit_variable" string => "private edit variable is $(parameter)"; 

  
  replace_patterns:

  # comment out lines after start

   "([^#].*)"

      replace_with => comment,
     select_region => ToEnd("Start.*");

  }

########################################
# Bodies
########################################

body replace_with comment

{
replace_value => "# $(match.1)"; # backreference 0
occurrences => "all";  # first, last all
}

########################################################

body select_region ToEnd(x)

{
select_start => "$(x)";
}
</pre>

<div class="node">
<a name="Service-management-(windows)"></a>
<a name="Service-management-_0028windows_0029"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Set-up-a-PXE-boot-server">Set up a PXE boot server</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Selecting-a-region-in-a-file">Selecting a region in a file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.53 Service management (windows)</h3>

<pre class="verbatim"></pre>

<pre class="verbatim"></pre>

<div class="node">
<a name="Set-up-a-PXE-boot-server"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Tidying-garbage-files">Tidying garbage files</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Service-management-_0028windows_0029">Service management (windows)</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.54 Set up a PXE boot server</h3>

<p>Use CFEngine to set up a PXE boot server.

<pre class="verbatim">#
# PXE boot server
#

bundle agent pxe
{
vars:

  "software" slist => {
                      "atftp",
                      "dhcp-server",
                      "syslinux",
                      "apache2"
                      };


   "dirs" slist => {
                   "/tftpboot",
                   "/tftpboot/CFEngine/rpm",
                   "/tftpboot/CFEngine/inputs",
                   "/tftpboot/pxelinux.cfg",
                   "/tftpboot/kickstart",
                   "/srv/www/repos"
                   };

   "tmp_location"    string => "/tftpboot/CFEngine/inputs";

   # Distros that we can install

   "rh_distros" slist => { "4.7", "5.2" };
   "centos_distros" slist => { "5.2" }; 

   # File contents of atftp configuration

   "atftpd_conf" string =>
       "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promising state.      ###
###########################################

ATFTPD_OPTIONS=\"--daemon \"
ATFTPD_USE_INETD=\"no\"
ATFTPD_DIRECTORY=\"/tftpboot\"
ATFTPD_BIND_ADDRESSES=\"\"
       ";

   # File contents of DHCP configuration

   "dhcpd" string =>
       "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promising state.      ###
###########################################

DHCPD_INTERFACE=\"eth0\"
DHCPD_RUN_CHROOTED=\"yes\"
DHCPD_CONF_INCLUDE_FILES=\"\"
DHCPD_RUN_AS=\"dhcpd\"
DHCPD_OTHER_ARGS=\"\"
DHCPD_BINARY=\"\"
       ";

   "dhcpd_conf" string =>
       "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promising state.      ###
###########################################

allow booting;
allow bootp;
ddns-update-style none; ddns-updates off;
 subnet 192.168.0.0 netmask 255.255.255.0 {
   range 192.168.0.20 192.168.0.254;
   default-lease-time 3600;
   max-lease-time 4800;
   option routers 192.168.0.1;
   option domain-name \"test.CFEngine.com\";
   option domain-name-servers 192.168.0.1;
   next-server 192.168.0.1;
   filename \"pxelinux.0\";
 }
 group {
   host node1 {
     # Dummy machine
     hardware ethernet 00:0F:1F:94:FE:07;
     fixed-address 192.168.0.11;
     option host-name \"node1\";
   }
   host node2 {
     # Dell Inspiron 1150
     hardware ethernet 00:0F:1F:0E:70:E7;
     fixed-address 192.168.0.12;
     option host-name \"node2\";
   }
 }
        ";

   # File contains of Apache2 HTTP configuration

   "httpd_conf" string =>
        "
# Repository for RHEL5
&lt;Directory /srv/www/repos>
Options Indexes 
AllowOverride None 
&lt;/Directory> 
Alias /repos /srv/www/repos

# PXE boot server
&lt;Directory /tftpboot/distro/RHEL/5.2>
Options Indexes  
AllowOverride None  
&lt;/Directory>  
Alias /distro/rhel/5.2 /tftpboot/distro/RHEL/5.2

&lt;Directory /tftpboot/distro/RHEL/4.7>
Options Indexes   
AllowOverride None   
&lt;/Directory>   
Alias /distro/rhel/4.7 /tftpboot/distro/RHEL/4.7

&lt;Directory /tftpboot/distro/CentOS/5.2>
Options Indexes    
AllowOverride None    
&lt;/Directory>    
Alias /distro/centos/5.2 /tftpboot/distro/CentOS/5.2    

&lt;Directory /tftpboot/kickstart>
Options Indexes     
AllowOverride None     
&lt;/Directory>     
Alias /kickstart /tftpboot/kickstart

&lt;Directory /tftpboot/CFEngine>
Options Indexes      
AllowOverride None      
&lt;/Directory>      
Alias /CFEngine /tftpboot/CFEngine
        ";

   # File contains of Kickstart for RHEL5 configuration

   "kickstart_rhel5_conf" string =>
        "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promissing state.     ###
###########################################
	
auth  --useshadow  --enablemd5 
bootloader --location=mbr
clearpart --all --initlabel 
graphical
firewall --disabled
firstboot --disable
key 77244a6377a8044a
keyboard no
lang en_US
logging --level=info
url --url=http://192.168.0.1/distro/rhel/5.2
network --bootproto=dhcp --device=eth0 --onboot=on
reboot
rootpw --iscrypted $1$eOnXdDPF$279sQ//zry6rnQktkATeM0
selinux --disabled
timezone --isUtc Europe/Oslo
install
part swap --bytes-per-inode=4096 --fstype=\"swap\" --recommended
part / --bytes-per-inode=4096 --fstype=\"ext3\" --grow --size=1

%packages
@core
@base
db4-devel
openssl-devel
gcc
flex
bison
libacl-devel
libselinux-devel
pcre-devel
#httpd
device-mapper-multipath
-sysreport

%post
cd /root
rpm -i http://192.168.0.1/CFEngine/rpm/CFEngine-3.0.1b1-1.el5.i386.rpm
#/sbin/chkconfig httpd on
cd /etc/yum.repos.d
wget http://192.168.0.1/repos/RHEL5.Base.repo
rpm --import /etc/pki/rpm-gpg/*
yum clean all
yum update
mkdir -p /root/CFEngine_init
cd /root/CFEngine_init
wget -nd -r http://192.168.0.1/CFEngine/inputs/
/usr/local/sbin/cf-agent -B
/usr/local/sbin/cf-agent
        ";

   # File contains of PXElinux boot menu

   "pxelinux_boot_menu" string =>
        "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promissing state.     ###
###########################################

boot options:
     rhel5   - install 32 bit i386 RHEL 5.2             (MANUAL)
     rhel5w  - install 32 bit i386 RHEL 5.2             (AUTO)
     rhel4   - install 32 bit i386 RHEL 4.7 AS          (MANUAL)    
     centos5 - install 32 bit i386 CentOS 5.2 (Desktop) (MANUAL)
        ";
   # File contains of PXElinux default configuration

   "pxelinux_default" string =>
        "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promissing state.     ###
###########################################
	
default rhel5
timeout 300
prompt 1
display pxelinux.cfg/boot.msg
F1 pxelinux.cfg/boot.msg

# install i386 RHEL 5.2
label rhel5
   kernel vmlinuz-RHEL5U2
   append initrd=initrd-RHEL5U2 load_ramdisk=1 ramdisk_size=16384 install=http://192.168.0.1/distro/rhel/5.2

# install i386 RHEL 5.2 using Kickstart
label rhel5w
   kernel vmlinuz-RHEL5U2
   append initrd=initrd-RHEL5U2 load_ramdisk=1 ramdisk_size=16384 ks=http://192.168.0.1/kickstart/kickstart-RHEL5U2.cfg

# install i386 RHEL 4.7
label rhel4
   kernel vmlinuz-RHEL4U7
   append initrd=initrd-RHEL4U7 load_ramdisk=1 ramdisk_size=16384 install=http://192.168.0.1/distro/rhel/4.7

# install i386 CentOS 5.2
label centos5
   kernel vmlinuz-CentOS5.2
   append initrd=initrd-CentOS5.2 load_ramdisk=1 ramdisk_size=16384 install=http://192.168.0.1/distro/centos/5.2
        ";

   # File contains of specified PXElinux default to be a RHEL5 webserver

   "pxelinux_rhel5_webserver" string =>
        "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promissing state.     ###
###########################################

# install i386 RHEL 5.2 using Kickstart
default rhel5w
label rhel5w
   kernel vmlinuz-RHEL5U2
   append initrd=initrd-RHEL5U2 load_ramdisk=1 ramdisk_size=16384 ks=http://192.168.0.1/kickstart/kickstart-RHEL5U2.cfg
        ";

   # File contains of a local repository for RHEL5

   "rhel5_base_repo" string =>
        "
###########################################
### This file is protected by CFEngine. ###
### Whatever you do, it will be changed ###
###     back to a promissing state.     ###
###########################################

# Local Repository
[Server]
name=Server
baseurl=http://192.168.0.1/repos/rhel5/Server/
enable=1
[VT]
name=VT
baseurl=http://192.168.0.1/repos/rhel5/VT/
enable=1
[Cluster]
name=Cluster
baseurl=http://192.168.0.1/repos/rhel5/Cluster/
enable=1
[ClusterStorage]
name=Cluster Storage
baseurl=http://192.168.0.1/repos/rhel5/ClusterStorage/
enable=1
        ";
#####################################################


files:

 packages_ok::

  # Create files/dirs and edit the new files

  "/tftpboot/distro/RHEL/$(rh_distros)/."
       create => "true";

  "/tftpboot/distro/CentOS/$(centos_distros)/."
       create => "true";

  "$(dirs)/."   
       create => "true";

  "/tftpboot/pxelinux.cfg/boot.msg"
       create => "true",
       perms => mo("644","root"),
       edit_line => append_if_no_line("$(pxelinux_boot_menu)"),
       edit_defaults => empty;

  "/tftpboot/pxelinux.cfg/default"
       create => "true",
       perms => mo("644","root"),
       edit_line => append_if_no_line("$(pxelinux_default)"),
       edit_defaults => empty;

  "/tftpboot/pxelinux.cfg/default.RHEL5.webserver"
       create => "true",
       perms => m("644","root"),
       edit_line => append_if_no_line("$(pxelinux_rhel5_webserver)"),
       edit_defaults => empty;
  
  "/tftpboot/kickstart/kickstart-RHEL5U2.cfg"
       create => "true",
       perms => m("644","root"),
       edit_line => append_if_no_line("$(kickstart_rhel5_conf)"),
       edit_defaults => empty;

  "/srv/www/repos/RHEL5.Base.repo"
       create => "true",
       perms => m("644","root"),
       edit_line => append_if_no_line("$(rhel5_base_repo)"),
       edit_defaults => empty;

  # Copy files

  "/tftpboot"

    copy_from => mycopy_local("/usr/share/syslinux","localhost"),
    depth_search => recurse("inf"),
    file_select => pxelinux_files,
    action => immediate;

  "$(tmp_location)"

    perms => m("644"),
    copy_from => mycopy("/var/cfengine/inputs","localhost"),
    depth_search => recurse("inf"),
    file_select => input_files,
    action => immediate;

  # Edit atftp, dhcp and apache2 configurations

  "/etc/sysconfig/atftpd"
     edit_line => append_if_no_line("$(atftpd_conf)"),
     edit_defaults => empty,
     classes => satisfied("atftpd_ready");

  "/etc/sysconfig/dhcpd"
     edit_line => append_if_no_line("$(dhcpd)"),
     edit_defaults => empty;

  "/etc/dhcpd.conf"
     edit_line => append_if_no_line("$(dhcpd_conf)"),
     edit_defaults => empty,
     classes => satisfied("dhcpd_ready");

  "/etc/apache2/httpd.conf"
     edit_line => append_if_no_line("$(httpd_conf)"),
     edit_defaults => def,
     classes => satisfied("apache2_ok");

  # Make a static link

  "/tftpboot/pxelinux.cfg/C0A8000C"
     link_from => mylink("/tftpboot/pxelinux.cfg/default.RHEL5.webserver");

  # Hash comment some lines for apaches

  apache2_ok::
  "/etc/apache2/httpd.conf"
     edit_line => comment_lines_matching_apache2("#"),
     classes => satisfied("apache2_ready");

commands:

  # Restart services
  atftpd_ready::
  "/etc/init.d/atftpd restart";

  dhcpd_ready::
  "/etc/init.d/dhcpd restart";

  apache2_ready::
  "/etc/init.d/apache2 restart";


#####################################################

packages:
  
 ipv4_192_168_0_1::
 # Only the PXE boot server

 "$(software)"

     package_policy => "add",
     package_method => zypper,
     classes => satisfied("packages_ok");

}

#####################################################
########### *** Bodies are here *** #################
#####################################################

body file_select pxelinux_files

{
leaf_name => { "pxelinux.0" };

file_result => "leaf_name";
}

#####################################################

body copy_from mycopy_local(from,server)

{
source      => "$(from)";
compare     => "digest";
}

#########################################################

body link_from mylink(x)
{
source => "$(x)";
link_type => "symlink";
}

#######################################################

body classes satisfied(new_class)

{
promise_kept => { "$(new_class)"};
promise_repaired => { "$(new_class)"};
}

#######################################################

bundle edit_line comment_lines_matching_apache2(comment)
  {
  
  vars:
   "regex" slist => { "\s.*Options\sNone", "\s.*AllowOverride\sNone", "\s.*Deny\sfrom\sall" };

  replace_patterns:

   "^($(regex))$"
      replace_with => comment("$(comment)");
  }

#######################################################

</pre>

<!--  -->
<div class="node">
<a name="Tidying-garbage-files"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Software-distribution">Software distribution</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Set-up-a-PXE-boot-server">Set up a PXE boot server</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.55 Tidying garbage files</h3>

<p>Emulating the `tidy' feature of CFEngine 2.

<pre class="verbatim">#######################################################
#
# Deleting files, like cf2 tidy age=0 r=inf
#
#######################################################

body common control

{
 any::

  bundlesequence  => { "testbundle" };   
}

############################################

bundle agent testbundle

{
files:

  "/tmp/test" 

    delete => tidyfiles,
    file_select => zero_age,
    depth_search => recurse("inf");
}

#########################################################

body depth_search recurse(d)

{
#include_basedir => "true";
depth => "$(d)";
}

#########################################################

body delete tidy

{
dirlinks => "delete";
rmdirs   => "false"; 
}

#########################################################

body file_select zero_age

#
# we can build old "include", "exclude", and "ignore" 
# from these as standard patterns - these bodies can
# form a library of standard patterns
#

{
mtime     => irange(ago(1,0,0,0,0,0),now);  
file_result => "mtime"; 
}

</pre>

<!--  -->
<ul class="menu">
<li><a accesskey="1" href="#Software-distribution">Software distribution</a>
<li><a accesskey="2" href="#Trigger-classes">Trigger classes</a>
</ul>

<div class="node">
<a name="Software-distribution"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Trigger-classes">Trigger classes</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Tidying-garbage-files">Tidying garbage files</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.56 Software distribution</h3>

<pre class="verbatim"></pre>

<!--  -->
<div class="node">
<a name="Trigger-classes"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Unmount-NFS-filesystem">Unmount NFS filesystem</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Software-distribution">Software distribution</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.57 Trigger classes</h3>

<pre class="verbatim">

#######################################################
#
# Insert a number of lines and trigger a followup if edited
#
#######################################################

body common control

{
any::

  bundlesequence  => { "insert" };   
}


#######################################################

bundle agent insert

{
vars:

  "v" string => "
                One potato
                Two potato
                Three potahto
                Four
                ";
 
files:

  "/tmp/test_insert"

     edit_line => Insert("$(insert.v)"),
     edit_defaults => empty,
     classes => trigger("edited");

commands:

 edited::

  "/bin/echo make bananas";

reports:

  edited::

    "The potatoes are bananas";

}

#######################################################
# For the library
#######################################################

bundle edit_line Insert(name)

{
insert_lines:

  "Begin$(const.n) $(name)$(const.n)End";

}

#######################################################

body edit_defaults empty

{
empty_file_before_editing => "true";
}

#######################################################

body classes trigger(x)

{
promise_repaired => { "$(x)" };
}
</pre>

<!--  -->
<div class="node">
<a name="Unmount-NFS-filesystem"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Web-server-modules">Web server modules</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Trigger-classes">Trigger classes</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.58 Unmount NFS filesystem</h3>

<pre class="verbatim">#####################################################################
# Mount NFS
#####################################################################

body common control

{
bundlesequence => { "mounts" };
}

#####################################################################

bundle agent mounts

{
storage:

  # Assumes the filesystem has been exported

  "/mnt" mount  => nfs("server.example.org","/home");
}

######################################################################

body mount nfs(server,source)

{
mount_type => "nfs";
mount_source => "$(source)";
mount_server => "$(server)";
edit_fstab => "true";
unmount => "true";
}
</pre>

<!--  -->
<div class="node">
<a name="Web-server-modules"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Warn-if-matching-line-in-file">Warn if matching line in file</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Unmount-NFS-filesystem">Unmount NFS filesystem</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.59 Web server modules</h3>

<p>The problem of editing the correct modules into the list of standard
modules for the Apache web server. This example is based on the
standard configuration deployment of SuSE Linux. Simply provide the
list of modules you want and another list that you don't want.

<pre class="verbatim">#######################################################
#
# Apache 2 reconfig - modelled on SuSE
#
#######################################################

body common control

{
any::

  bundlesequence  => {
                     apache
                     };   
}

#######################################################

bundle agent apache

{
files:

 SuSE::

  "/etc/sysconfig/apache2" 

     edit_line => fixapache;
}

#######################################################
# For the library
#######################################################

bundle edit_line fixapache

{ 
vars:

 "add_modules"     slist => { 
                            "dav", 
                            "dav_fs", 
                            "ssl", 
                            "php5", 
                            "dav_svn",
                            "xyz",
                            "superduper"
                            };

 "del_modules"     slist => { 
                            "php3",
                            "jk",
                            "userdir",
                            "imagemap",
                            "alias"
                            };

insert_lines:

 "APACHE_CONF_INCLUDE_FILES=\"/site/masterfiles/local-http.conf\"";

field_edits:

 #####################################################################
 # APACHE_MODULES="authz_host actions alias ..."
 #####################################################################

    # Values have the form NAME = "quoted space separated list"

   "APACHE_MODULES=.*"

      # Insert module "columns" between the quoted RHS 
      # using space separators

      edit_field => quotedvar("$(add_modules)","append");

   "APACHE_MODULES=.*"

      # Delte module "columns" between the quoted RHS 
      # using space separators

      edit_field => quotedvar("$(del_modules)","delete");

   # if this line already exists, edit it  

}

</pre>

<!--  -->
<ul class="menu">
<li><a accesskey="1" href="#Warn-if-matching-line-in-file">Warn if matching line in file</a>
<li><a accesskey="2" href="#Windows-registry">Windows registry</a>
<li><a accesskey="3" href="#unit_005fregistry_005fcache_002ecf">unit_registry_cache.cf</a>
<li><a accesskey="4" href="#unit_005fregistry_002ecf">unit_registry.cf</a>
</ul>

<div class="node">
<a name="Warn-if-matching-line-in-file"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#Windows-registry">Windows registry</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Web-server-modules">Web server modules</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.60 Warn if matching line in file</h3>

<pre class="verbatim">
########################################################
#
# Warn if line matched
#
########################################################

body common control

{
bundlesequence  => { "testbundle" };
}

########################################################

bundle agent testbundle

{
files:

  "/var/cfengine/inputs/.*"

       edit_line => DeleteLinesMatching(".*cfenvd.*"),
       action => WarnOnly;
}

########################################################

bundle edit_line DeleteLinesMatching(regex)
  {
  delete_lines:

    "$(regex)" action => WarnOnly;

  }

########################################################

body action WarnOnly
{
action_policy => "warn";
}
</pre>

<div class="node">
<a name="Windows-registry"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#unit_005fregistry_005fcache_002ecf">unit_registry_cache.cf</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Warn-if-matching-line-in-file">Warn if matching line in file</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.61 Windows registry</h3>

<pre class="verbatim">

body common control
{
bundlesequence => { "reg" };
}

bundle agent reg
{
vars:

  "value" string => registryvalue("HKEY_LOCAL_MACHINE\SOFTWARE\Cfengine AS\Cfengine","value3");

reports:

  windows::

   "Value extracted: $(value)";

}
</pre>

<div class="node">
<a name="unit_registry_cache.cf"></a>
<a name="unit_005fregistry_005fcache_002ecf"></a>
<p><hr>
Next:&nbsp;<a rel="next" accesskey="n" href="#unit_005fregistry_002ecf">unit_registry.cf</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="#Windows-registry">Windows registry</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.62 unit_registry_cache.cf</h3>

<pre class="verbatim"></pre>

<div class="node">
<a name="unit_registry.cf"></a>
<a name="unit_005fregistry_002ecf"></a>
<p><hr>
Previous:&nbsp;<a rel="previous" accesskey="p" href="#unit_005fregistry_005fcache_002ecf">unit_registry_cache.cf</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="#Low-level">Low level</a>

</div>

<h3 class="section">3.63 unit_registry.cf</h3>

<pre class="verbatim">



body common control
{
 
bundlesequence => { "databases" };
}


bundle agent databases

{
databases:

 windows::

  # Regsitry has (value,data) pairs in "keys" which are directories

#  "HKEY_LOCAL_MACHINE\SOFTWARE\Cfengine AS"

#    database_operation => "create", 
#    database_type     => "ms_registry";

#  "HKEY_LOCAL_MACHINE\SOFTWARE\Cfengine AS\Cfengine"

#    database_operation => "create", 
#    database_rows => { "value1,REG_SZ,new value 1", "value2,REG_SZ,new val 2"} ,
#    database_type     => "ms_registry";


  "HKEY_LOCAL_MACHINE\SOFTWARE\Cfengine AS\Cfengine"

    database_operation => "delete", 
    database_columns => { "value1", "value2" } ,
    database_type => "ms_registry";


# "HKEY_LOCAL_MACHINE\SOFTWARE\Cfengine AS\Cfengine"

#    database_operation => "cache",   # cache,restore

#    registry_exclude => { ".*Windows.*CurrentVersion.*", ".*Touchpad.*", ".*Capabilities.FileAssociations.*", ".*Rfc1766.*" , ".*Synaptics.SynTP.*", ".*SupportedDevices.*8086", ".*Microsoft.*ErrorThresholds" },

#    database_type     => "ms_registry";

"HKEY_LOCAL_MACHINE\SOFTWARE\Cfengine AS"

   database_operation => "restore",
   database_type      => "ms_registry";

}
</pre>

<!-- ========================================================================= -->
<!-- @node Index,  , CFEngine Methods, Top -->
<!-- @unnumbered Concept Index -->
<!-- @printindex cp -->
<!-- ========================================================================= -->
<p><script type="text/javascript">
var gaJsHost = (("https:" == document.location.protocol) ? "https://ssl." : "http://www.");
document.write(unescape("%3Cscript src='" + gaJsHost + "google-analytics.com/ga.js' type='text/javascript'%3E%3C/script%3E"));
</script>
<script type="text/javascript">
var pageTracker = _gat._getTracker("UA-2576171-2");
pageTracker._initData();
pageTracker._trackPageview();
</script>

<div class="footnote">
<hr>
<a name="texinfo-footnotes-in-document"></a><h4>Footnotes</h4><p class="footnote"><small>[<a name="fn-1" href="#fnd-1">1</a>]</small> In CFEngine 2
there is a separate action type for configuring the system
resolver. In CFEngine 3 this has been deprecated for this standard
method using the basic functionality of CFEngine.</p>

   <hr></div>

</body></html>

