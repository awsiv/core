#
#  Copyright (C) CFEngine AS
# 
#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU Lesser General Public License LGPL as published by the
#  Free Software Foundation; version 3.
#   
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  To the extent this program is licensed as part of the Enterprise
#  versions of CFEngine, the applicable Commerical Open Source License
#  (COSL) may apply to this file if you as a licensee so wish it. See
#  included file COSL.txt.
#
#######################################################
# Name Resolution (/etc/resolv.conf)
#######################################################
#
# CATEGORY:
#  Networking
#
# LICENSE:
#  COPBL
#
# PLATFORMS:
#  Linux, UNIX
#
# DESCRIPTION:
#  Ensure that /etc/resolv.conf have right contents
#
# REQUIREMENT:
#  CFEngine version 3.x.x
#  Public/Private software repository
#  inputs => {
#             "networking/net_name_resolution.cf", 
#             "cfengine_stdlib.cf",
#             };
# 
# USAGE:
#  In bundlesequence/methods, do like the following;
#
#   bundlesequence => {
#                       net_name_resolution("$(search)","$(nameservers)"),
#                     };
#
# or
#
#  methods:
#   "RESOLVER" usebundle => net_name_resolution("$(search)","$(nameservers)");
#
#    $(search)      => a search domain
#    $(nameservers) => a list of nameservers' IP addresses, can be upto 3 IPs sepearating by "," (comma)
#
#   for example; 
#      net_name_resolution("cfengine.com","111.111.111.111,222.222.222.22");
#    
#
# CHANGES:
#  
#
# Author:
#  Nakarin Phooripoom
#
#
#

bundle agent net_name_resolution(search,nameservers)
{
 vars:

  !windows::

   "split"   slist => splitstring("$(nameservers)",",","3"),
           comment => "Prepare inputs of nameservers from string to a list",
            handle => "net_name_resolution_vars_split_nameservers";

#

 files:

  !windows::

   "$(sys.resolv)"
           comment => "Ensure contents in the resolver DSN client configuration",
            handle => "net_name_resolution_files_etc_resolv_conf",
           create  => "true",
         edit_line => resolver("$(search)","$(split)"),
     edit_defaults => std_defs;

}

#######################################################

bundle edit_line resolver(sname,nsname)
{
 delete_lines:
 
   "search.*"
      comment => "Delete old domain search configuration",
       handle => "resolver_delete_lines_invalid_search";
  
   "nameserver ([0-9]*\.){0,2}[^\.]*"
      comment => "Delete invalid format of nameserver",
       handle => "resolver_delete_lines_invalid_nameservers";

 # to delete specific nameservers can be added here
 #  "nameserver 123.123.123.123";

 insert_lines:

   "search $(sname)"
      comment => "To append a search domain line from INPUTS",
       handle => "resolver_insert_lines_add_search",
      location => start;

  any::

   "nameserver $(nsname)"
      comment => "To append nameserver lines from INPUTS",
       handle => "resolver_insert_lines_add_nameservers";
}
