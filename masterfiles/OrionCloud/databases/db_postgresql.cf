#
#  Copyright (C) CFEngine AS
# 
#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU Lesser General Public License LGPL as published by the
#  Free Software Foundation; version 3.
#   
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  To the extent this program is licensed as part of the Enterprise
#  versions of CFEngine, the applicable Commerical Open Source License
#  (COSL) may apply to this file if you as a licensee so wish it. See
#  included file COSL.txt.
#
#######################################################
# PostgreSQL
#######################################################
#
# CATEGORY:
#  Databases
#
# LICENSE:
#  COPBL
#
# PLATFORMS:
#  Redhat, CentOS, Fedora, Debian, Ubuntu, SUSE
#
# DESCRIPTION:
#  Install PostgreSQL packages and ensure that the process running
#
# REQUIREMENT:
#  CFEngine version 3.x.x
#  Public/Private software repository
#  inputs => {
#             "databases/db_postgresql.cf", 
#             "cfengine_stdlib.cf",
#             };
# 
# USAGE:
#  In bundlesequence/methods, do like the following;
#
#   bundlesequence => {
#                       db_postgresql("$(type)","$(state)"),
#                     };
#
# or
#
#  methods:
#   "PostgreSQL" usebundle => db_postgresql("$(type)","$(state)");
#
#    $(type)  = "client" -> to select only PostgreSQL client packages
#    $(type)  = "server" -> to select both PostgreSQL client and server packages  
#    $(state) = "on"     -> to install PostgreSQL database
#    $(state) = "purge"  -> to uninstall PostgreSQL database
#    
#
# CHANGES:
#  
#
# Author:
#  Nakarin Phooripoom
#
#
#

bundle agent db_postgresql(type,state)
{
 vars:

  redhat|centos|fedora|suse|SuSE::

   "postgresql_pkgs"      slist => {
                                    "postgresql",
                                    "postgresql-libs",
                                   },
                        comment => "List of PostgreSQL client packages on Redhat-related/SUSE distributions",
                         handle => "db_postgresql_vars_postgresql_pkgs_client_redhat_centos_fedora_suse",
                     ifvarclass => "client";

   "postgresql_pkgs"      slist => {
                                    "postgresql",
                                    "postgresql-libs",
                                    "postgresql-server",
                                   },
                        comment => "List of PostgreSQL server packages on Redhat-related/SUSE distributions",
                         handle => "db_postgresql_vars_postgresql_pkgs_server_redhat_centos_fedora_suse",
                     ifvarclass => "server";

  debian|ubuntu::

   "postgresql_pkgs"      slist => {
                                    "postgresql-client-8.4",
                                    "postgresql-client-common",
                                    "postgresql-doc-8.4",
                                    "libpq5",
                                   },
                        comment => "List of PostgreSQL client packages on Debian-related distributions",
                         handle => "db_postgresql_vars_postgresql_pkgs_client_debian_ubuntu",
                     ifvarclass => "client";

   "postgresql_pkgs"      slist => {
                                    "postgresql-8.4",
                                    "postgresql-common",
                                    "postgresql-client-8.4",
                                    "postgresql-client-common",
                                    "postgresql-doc-8.4",
                                    "libpq5",
                                   },
                        comment => "List of PostgreSQL server packages on Debian-related distributions",
                         handle => "db_postgresql_vars_postgresql_pkgs_server_debian_ubuntu",
                     ifvarclass => "server";
#

 classes:

  any::

   "on"     expression => strcmp("$(state)","on"),
               comment => "Check if to install packages",
                handle => "db_postgresql_classes_strcmp_on";
   "purge"  expression => strcmp("$(state)","purge"),
               comment => "Check if to remove packages",
                handle => "db_postgresql_classes_strcmp_purge";
   "client" expression => strcmp("$(type)","client"),
               comment => "Check if to install only client packages",
                handle => "db_postgresql_classes_strcmp_client";
   "server" expression => strcmp("$(type)","server"),
               comment => "Check if to install both client and server packages",
                handle => "db_postgresql_classes_strcmp_server";

#

 packages:

  on::

   "$(postgresql_pkgs)"

             comment => "Install PostgreSQL packages",
              handle => "db_postgresql_packages_add",
      package_policy => "add",
      package_method => generic,
             classes => if_ok("ensure_postgresql_running"),
              action => log_repaired("stdout","PostgreSQL $(type) was installed");

  purge::

   "$(postgresql_pkgs)"

             comment => "Remove PostgreSQL packages",
              handle => "db_postgresql_packages_remove",
      package_policy => "delete",
      package_method => generic,
              action => log_repaired("stdout","PostgreSQL $(type) was removed");

#

 processes:

  (redhat|centos|fedora|suse|SuSE).server.ensure_postgresql_running::

   "/usr/bin/postmaster.*"
            comment => "Check for postgresql process on Redhat-related and SUSE distributions",
             handle => "db_postgresql_processes_run_redhat_centos_fedora_suse",
      restart_class => "redhat_centos_fedora_suse_start_postgresql";

  (debian|ubuntu).server.ensure_postgresql_running::

   ".*postgres.*"
            comment => "Check for postgresql process on Debian-related distributions",
             handle => "db_postgresql_processes_run_debian_ubuntu",
      restart_class => "debian_ubuntu_start_postgresql";

#

 commands:

  redhat_centos_fedora_suse_start_postgresql::

   "/etc/init.d/postgresql restart"
      comment => "Start/Restart command of PostgreSQL on Redhat-related and SUSU distributions",
       handle => "db_postgresql_commands_run_redhat_centos_fedora_suse";

  debian_ubuntu_start_postgresql::

   "/etc/init.d/postgresql-8.4 restart"
      comment => "Start/Restart command of PostgreSQL on Debian-related distributions",
       handle => "db_postgresql_commands_run_debian_ubuntu";

}
