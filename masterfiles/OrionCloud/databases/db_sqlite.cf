#
#  Copyright (C) CFEngine AS
# 
#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU Lesser General Public License LGPL as published by the
#  Free Software Foundation; version 3.
#   
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  To the extent this program is licensed as part of the Enterprise
#  versions of CFEngine, the applicable Commerical Open Source License
#  (COSL) may apply to this file if you as a licensee so wish it. See
#  included file COSL.txt.
#
#######################################################
# SQLite
#######################################################
#
# CATEGORY:
#  Databases
#
# LICENSE:
#  COPBL
#
# PLATFORMS:
#  Redhat, CentOS, Fedora, Debian, Ubuntu, SUSE
#
# DESCRIPTION:
#  Install SQLlite packages
#
# REQUIREMENT:
#  CFEngine version 3.x.x
#  Public/Private software repository
#  inputs => {
#             "databases/db_sqlite.cf", 
#             "cfengine_stdlib.cf",
#             };
# 
# USAGE:
#  In bundlesequence/methods, do like the following;
#
#   bundlesequence => {
#                       db_sqlite("$(state)"),
#                     };
#
# or
#
#  methods:
#   "SQLite" usebundle => db_sqlite("$(state)");
#
#    $(state) = "on"    -> to install SQLlite
#    $(state) = "purge" -> to uninstall SQLite
#    
#
# CHANGES:
#  - exclude redhat not to purge SQLite packages as rpm/yum depend on libsqlite3.so.0
#  
#
# Author:
#  Nakarin Phooripoom
#
#
#

bundle agent db_sqlite(state)
{
 vars:

  redhat|centos|fedora::

   "sqlite_pkgs"   slist => {
                             "sqlite",
                             "sqlite-devel",
                            },
                 comment => "List of SQLite packages on Redhat-related distributions",
                  handle => "db_sqlite_vars_sqlite_pkgs_redhat_centos_fedora";

  debian|ubuntu::

   "sqlite_pkgs"   slist => {
                             "sqlite3",
                             "libsqlite3-dev",
                             "sqlite3-doc",
                            },
                 comment => "List of SQLite packages on Debian-related distributions",
                  handle => "db_sqlite_vars_sqlite_pkgs_debian_ubuntu";

  suse|SuSE::

   "sqlite_pkgs"   slist => {
                             "sqlite3",
                             "sqlite3-devel",
                            },
                 comment => "List of SQLite packages on SUSE",
                  handle => "db_sqlite_vars_sqlite_pkgs_suse";

#

 classes:

  any::

   "on"    expression => strcmp("$(state)","on"),
              comment => "Check if to install packages",
               handle => "db_sqlite_classes_strcmp_on";
   "purge" expression => strcmp("$(state)","purge"),
              comment => "Check if to remove packages",
               handle => "db_sqlite_classes_strcmp_purge";

#


 packages:

  on::

   "$(sqlite_pkgs)"

             comment => "Install SQLite packages",
              handle => "db_sqlite_packages_add",
      package_policy => "add",
      package_method => generic,
              action => log_repaired("stdout","SQLite was installed");

  purge.!redhat::

   "$(sqlite_pkgs)"

             comment => "Remove SQLite packages, but not for Redhat-related distributions because rmp/yum rely on libsqlite.so.0",
              handle => "db_sqlite_packages_remove",
      package_policy => "delete",
      package_method => generic,
              action => log_repaired("stdout","SQLite was removed");
}
